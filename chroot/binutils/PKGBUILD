pkgname=chroot-binutils
pkgver=2.31.1
pkgrel=1
arch=('x86_64' 'i586' 'aarch64' 'arm' 'riscv32' 'riscv64')
source=(http://ftpmirror.gnu.org/gnu/binutils/binutils-$pkgver.tar.xz
	0001-PR23428-x86-Add-a-GNU_PROPERTY_X86_ISA_1_USED-note-if-needed.patch
	0002-PR23460-Close-resource-leaks-in-the-BFD-library-s-plugin-han.patch
	0003-PR23460-Add-a-testcase-for-PR-binutils-23460.patch
	0004-PR23486-Properly-merge-GNU_PROPERTY_X86_ISA_1_USED-x86_64.patch
	0005-PR23486-x86-Properly-merge-GNU_PROPERTY_X86_ISA_1_USED.patch
	0006-PR23428-x86-Properly-add-X86_ISA_1_NEEDED-property.patch)

prepare() {
	cd binutils-$pkgver
	patch -Np1 -i $srcdir/0001-PR23428-x86-Add-a-GNU_PROPERTY_X86_ISA_1_USED-note-if-needed.patch
	patch -Np1 -i $srcdir/0004-PR23486-Properly-merge-GNU_PROPERTY_X86_ISA_1_USED-x86_64.patch
	patch -Np1 -i $srcdir/0005-PR23486-x86-Properly-merge-GNU_PROPERTY_X86_ISA_1_USED.patch
	patch -Np1 -i $srcdir/0006-PR23428-x86-Properly-add-X86_ISA_1_NEEDED-property.patch
	patch -Np1 -i $srcdir/0002-PR23460-Close-resource-leaks-in-the-BFD-library-s-plugin-han.patch
	patch -Np1 -i $srcdir/0003-PR23460-Add-a-testcase-for-PR-binutils-23460.patch

	mkdir build
}

build() {
	cd binutils-$pkgver
	cd build
	../configure \
		--prefix=/tools \
		--build=$XHOST \
		--host=$XTARGET \
		--target=$XTARGET $BINUTILSOPTS \
		--with-bugurl='https://github.com/minitena/issues/issues' \
		--with-lib-path=/tools/lib \
		--with-pic \
		--with-system-zlib \
		--enable-64-bit-bfd \
		--enable-deterministic-archives \
		--enable-gold=yes \
		--enable-plugins \
		--enable-relro \
		--enable-shared \
		--enable-threads \
		--disable-multilib \
		--disable-nls \
		--disable-werror
	make MAKEINFO="true"
}

package() {
	cd binutils-$pkgver
	cd build
	make MAKEINFO="true" DESTDIR=$pkgdir install

	mkdir -p ld-new-dir
	make -C ld clean
	make -C ld LIB_PATH=/usr/lib
	make -C ld LIB_PATH=/usr/lib DESTDIR=$(pwd)/ld-new-dir install
	cp ld-new-dir/tools/bin/ld $pkgdir/tools/bin/ld-new
}
