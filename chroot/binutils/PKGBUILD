pkgname=chroot-binutils
pkgver=2.31.1
pkgrel=1
arch=('x86_64' 'i586' 'aarch64' 'arm' 'mips64' 'mips' 'powerpc64le' 'powerpc64' 'powerpc' 'sparc64' 'sparc' 's390x' 'microblaze' 'riscv64' 'riscv32')
source=(http://ftpmirror.gnu.org/gnu/binutils/binutils-$pkgver.tar.xz)

prepare() {
	cd binutils-$pkgver
	mkdir build
}

build() {
	cd binutils-$pkgver
	cd build
	../configure \
		--prefix=/tools \
		--build=$XHOST \
		--host=$XTARGET \
		--target=$XTARGET $BINUTILSOPTS \
		--with-bugurl='https://github.com/minitena/source/issues' \
		--with-lib-path=/tools/lib \
		--with-pic \
		--with-system-zlib \
		--enable-64-bit-bfd \
		--enable-deterministic-archives \
		--enable-gold=yes \
		--enable-lto \
		--enable-plugins \
		--enable-relro \
		--enable-shared \
		--enable-threads \
		--disable-multilib \
		--disable-nls \
		--disable-werror
	make MAKEINFO="true"
}

package() {
	cd binutils-$pkgver
	cd build
	make MAKEINFO="true" DESTDIR=$pkgdir install

	mkdir -p ld-new-dir
	make -C ld clean
	make -C ld LIB_PATH=/usr/lib
	make -C ld LIB_PATH=/usr/lib DESTDIR=$(pwd)/ld-new-dir install
	cp ld-new-dir/tools/bin/ld $pkgdir/tools/bin/ld-new
}
