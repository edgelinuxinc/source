pkgname=chroot-gcc
pkgver=8.2.1+20180831
gmpver=6.1.2
mpfrver=4.0.1
mpcver=1.1.0
islver=0.20
pkgrel=1
arch=('x86_64' 'aarch64')
source=(https://sources.archlinux.org/other/gcc/gcc-${pkgver/+/-}.tar.xz
	http://ftpmirror.gnu.org/gnu/gmp/gmp-$gmpver.tar.xz
	http://www.mpfr.org/mpfr-$mpfrver/mpfr-$mpfrver.tar.xz
	http://ftpmirror.gnu.org/gnu/mpc/mpc-$mpcver.tar.gz
	http://isl.gforge.inria.fr/isl-$islver.tar.xz)

prepare() {
	cd gcc-${pkgver/+/-}
	sed -i 's@\./fixinc\.sh@-c true@' gcc/Makefile.in
	sed -i '/m64=/s/lib64/lib/' gcc/config/i386/t-linux64
	sed -i '/lp64=/s/lib64/lib/' gcc/config/aarch64/t-aarch64-linux

	for file in gcc/config/linux.h gcc/config/i386/linux.h gcc/config/i386/linux64.h \
			gcc/config/aarch64/aarch64-linux.h; do
		cp -u $file{,.orig}
		sed -e 's@/lib\(64\)\?\(32\)\?/ld@/tools&@g' \
			-e 's@/usr@/tools@g' $file.orig > $file
		echo '
	#undef STANDARD_STARTFILE_PREFIX_1
	#undef STANDARD_STARTFILE_PREFIX_2
	#define STANDARD_STARTFILE_PREFIX_1 "/tools/lib/"
	#define STANDARD_STARTFILE_PREFIX_2 ""' >> $file
		touch $file.orig
	done

	mv ../gmp-$gmpver gmp
	mv ../mpfr-$mpfrver mpfr
	mv ../mpc-$mpcver mpc
	mv ../isl-$islver isl

	mkdir build
}

build() {
	export CFLAGS="${CFLAGS/-Os/-O3}"
	export CXXFLAGS="${CXXFLAGS/-Os/-O3}"

	cd gcc-${pkgver/+/-}
	cd build
	../configure \
		--prefix=/tools \
		--libdir=/tools/lib \
		--build=$XHOST \
		--host=$XTARGET \
		--target=$XTARGET $GCCOPTS \
		--with-pkgversion='Minitena' \
		--with-bugurl='https://github.com/minitena/issues/issues' \
		--with-local-prefix=/tools \
		--with-native-system-header-dir=/tools/include \
		--with-system-zlib \
		--enable-__cxa_atexit \
		--enable-checking=release \
		--enable-clocale=gnu \
		--enable-default-pie \
		--enable-default-ssp \
		--enable-gnu-indirect-function \
		--enable-gnu-unique-object \
		--enable-languages=c,c++ \
		--enable-install-libiberty \
		--enable-linker-build-id \
		--enable-shared \
		--enable-threads=posix \
		--disable-libmpx \
		--disable-libstdcxx-pch \
		--disable-libunwind-exceptions \
		--disable-multilib \
		--disable-nls \
		--disable-werror
	make AS_FOR_TARGET="$AS" LD_FOR_TARGET="$LD"
}

package() {
	cd gcc-${pkgver/+/-}
	cd build
	make -j1 DESTDIR=$pkgdir install

	ln -sf gcc $pkgdir/tools/bin/cc
}
