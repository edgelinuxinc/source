pkgname=chroot-glibc
pkgver=2.28
pkgrel=1
arch=('x86_64' 'i586' 'aarch64' 'armv7h' 'armv6h' 'armv5tel' 'powerpc64le' 'powerpc64' 'powerpc' 'sparc64' 's390x' 'riscv64')
source=(http://ftpmirror.gnu.org/gnu/glibc/glibc-$pkgver.tar.xz)

prepare() {
	cd glibc-$pkgver
	mkdir build
	cd build

	echo "slibdir=/tools/lib" >> configparms
	echo "rtlddir=/tools/lib" >> configparms
	echo "sbindir=/tools/bin" >> configparms
	echo "rootsbindir=/tools/bin" >> configparms
}

build() {
	export CFLAGS="${CFLAGS/-fno-plt}"
	export CXXFLAGS="${CXXFLAGS/-fno-plt}"
	export CFLAGS="${CFLAGS/-D_FORTIFY_SOURCE=2}"
	export CXXFLAGS="${CXXFLAGS/-D_FORTIFY_SOURCE=2}"
	export CFLAGS="${CFLAGS/-Os/-O3}"
	export CXXFLAGS="${CXXFLAGS/-Os/-O3}"

	cd glibc-$pkgver
	cd build

	BUILD_CC="$HOSTCC" \
	CC="$XTARGET-gcc" \
	AR="$XTARGET-ar" \
	RANLIB="$XTARGET-ranlib" \
	../configure \
		--prefix=/tools \
		--libdir=/tools/lib \
		--build=$XHOST \
		--host=$XTARGET $GLIBCOPTS \
		--with-pkgversion="Minitena $pkgver" \
		--with-bugurl='https://github.com/minitena/source/issues' \
		--with-binutils=$TOOLS/bin \
		--with-headers=$ROOTFS/tools/include \
		--without-gd \
		--without-selinux \
		--enable-add-ons \
		--enable-bind-now \
		--enable-kernel=3.12.0 \
		--enable-lock-elision \
		--enable-stack-protector=strong \
		--enable-stackguard-randomization \
		--disable-profile \
		--disable-werror \
		libc_cv_forced_unwind=yes \
		libc_cv_c_cleanup=yes
	make
}

package() {
	cd glibc-$pkgver
	cd build
	make install_root=$pkgdir install
}
