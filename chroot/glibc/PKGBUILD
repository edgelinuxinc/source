pkgname=chroot-glibc
pkgver=2.28
pkgrel=1
arch=('x86_64' 'aarch64')
source=(http://ftpmirror.gnu.org/gnu/glibc/glibc-$pkgver.tar.xz
	bz20338.patch
	bz23497.patch
	0001-Revert-elf-Correct-absolute-SHN_ABS-symbol-run-time-.patch)

prepare() {
	cd glibc-$pkgver
	patch -Np1 -i $srcdir/bz20338.patch
	patch -Np1 -i $srcdir/bz23497.patch
	patch -Np1 -i $srcdir/0001-Revert-elf-Correct-absolute-SHN_ABS-symbol-run-time-.patch

	mkdir build
	cd build

	echo "slibdir=/tools/lib" >> configparms
	echo "rtlddir=/tools/lib" >> configparms
	echo "sbindir=/tools/bin" >> configparms
	echo "rootsbindir=/tools/bin" >> configparms
}

build() {
	cd glibc-$pkgver
	cd build

	CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/} \
	BUILD_CC="$HOSTCC" \
	CC="$XTARGET-gcc" \
	AR="$XTARGET-ar" \
	RANLIB="$XTARGET-ranlib" \
	../configure \
		--prefix=/tools \
		--libdir=/tools/lib \
		--build=$XHOST \
		--host=$XTARGET \
		--with-pkgversion='Edge Linux' \
		--with-bugurl='https://github.com/edgelinuxinc/issues/issues' \
		--with-binutils=$TOOLS/bin \
		--with-headers=$ROOTFS/tools/include \
		--enable-add-ons \
		--enable-bind-now \
		--enable-kernel=3.12.0 \
		--enable-lock-elision \
		--enable-multi-arch \
		--enable-stack-protector=strong \
		--enable-stackguard-randomization \
		--enable-static-pie \
		--disable-profile \
		--disable-werror \
		libc_cv_forced_unwind=yes \
		libc_cv_c_cleanup=yes
	make
}

package() {
	cd glibc-$pkgver
	cd build
	make install_root=$pkgdir install
}
