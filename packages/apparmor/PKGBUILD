# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=apparmor
pkgver=2.13
pkgrel=1
pkgdesc="Mandatory Access Control (MAC) using Linux Security Module (LSM)"
arch=('x86_64' 'i586' 'aarch64')
url="https://gitlab.com/apparmor/apparmor"
license=('GPL')
depends=('glibc' 'pam' 'python' 'audit')
backup=('etc/apparmor/easyprof.conf'
	'etc/apparmor/logprof.conf'
	'etc/apparmor/notify.conf'
	'etc/apparmor/parser.conf'
	'etc/apparmor/subdomain.conf'
	'etc/apparmor/severity.db')
source=("https://launchpad.net/apparmor/$pkgver/$pkgver.0/+download/$pkgname-$pkgver.tar.gz")
md5sums=('c6caefb0a558492082226c467f6954cb')

build() {
	cd $pkgname-$pkgver
	sed -e 's/sbin/usr\/bin/g' \
		-e 's/\}\/lib\/apparmor/\}\/usr\/lib\/apparmor/' \
		-e 's/644 apparmor.systemd/755 apparmor.systemd/' \
		-i parser/Makefile

	cd libraries/libapparmor

	export PYTHON_VERSION=3
	export PYTHON_VERSIONS=python3
	export PYTHON=/usr/bin/python3

	./autogen.sh
	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--with-perl \
		--with-python \
		--build=$CHOST
	make

	cd $srcdir/$pkgname-$pkgver
	make -C binutils
	make -C parser
	make -C profiles
	make -C utils
	make -C changehat/pam_apparmor
	make -C utils/vim
}

package() {
	mkdir -p $pkgdir/usr/lib/apparmor

	cd $pkgname-$pkgver
	make -C libraries/libapparmor DESTDIR=$pkgdir install
	make -C changehat/pam_apparmor DESTDIR=$pkgdir/usr install
	make -C binutils DESTDIR=$pkgdir install
	make -C parser DESTDIR=$pkgdir install
	make -C parser DESTDIR=$pkgdir install-systemd
	make -C profiles DESTDIR=$pkgdir install
	make -C utils DESTDIR=$pkgdir BINDIR=$pkgdir/usr/bin install

	find $pkgdir/usr/lib/perl5/ \
		-type f -iname "*.so" \
		-exec strip --strip-unneeded {} \; \
		-exec chrpath -d {} \;

	cd $pkgdir
	[[ /usr/bin/true ]] && backup=( ${backup[@]} $(find "etc/${pkgname}.d/" -type f) )
}
