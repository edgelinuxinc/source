# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=argon2
pkgver=20171227
pkgrel=1
pkgdesc="A password-hashing function (reference C implementation)"
arch=('x86_64' 'i586' 'aarch64' 'arm' 'mips64' 'mips' 'powerpc64le' 'powerpc64' 'powerpc' 'sparc64' 'sparc' 's390x' 'microblaze' 'riscv64' 'riscv32')
url="https://github.com/P-H-C/phc-winner-argon2"
license=('Apache' 'custom')
depends=('glibc')
source=("https://github.com/P-H-C/phc-winner-argon2/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz"
	'libargon2.pc')
md5sums=('7d0a85aa3fa02a5962ff751a6e2078c8'
         'd61611be3e0409008ac2d3f3419a25e0')

prepare() {
	cd phc-winner-$pkgname-$pkgver

	cp $srcdir/libargon2.pc libargon2.pc
	sed -i -e "s/@UPSTREAM_VER@/${pkgver}/" libargon2.pc
}

build() {
	cd phc-winner-$pkgname-$pkgver
	make OPTTARGET=none
}

package() {
	cd phc-winner-$pkgname-$pkgver
	make OPTTARGET=none DESTDIR=$pkgdir install

	install -D -m0644 libargon2.pc $pkgdir/usr/lib/pkgconfig/libargon2.pc
}
