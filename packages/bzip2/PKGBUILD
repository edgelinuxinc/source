# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=bzip2
pkgver=1.0.6
pkgrel=2
pkgdesc="A high-quality data compression program"
arch=('x86_64' 'i586' 'aarch64' 'armv7h' 'armv6h' 'armv5tel' 'powerpc64le' 'powerpc64' 'powerpc' 'sparc64' 's390x' 'riscv64')
url="http://sources.redhat.com/bzip2"
license=('custom')
depends=('glibc')
source=("https://sources.archlinux.org/other/packages/$pkgname/$pkgname-$pkgver.tar.gz"
	'bzip2-1.0.4-bzip2recover.patch'
	'bzip2-1.0.6-CVE-2016-3189.patch')
md5sums=('00b516f4704d4a7cb50a1d97e6e8e15b'
         'a9aee693365c1acfccff5592aa19d527'
         '9b2dab91fc059ea8f90f25afaa30c04b')

prepare() {
	cd $pkgname-$pkgver
	sed -e 's/^CFLAGS=\(.*\)$/CFLAGS=\1 \$(BIGFILES)/' -i ./Makefile-libbz2_so

	sed -i "s|-O2|${CFLAGS} -fPIC|g" Makefile
	sed -i "s|-O2|${CFLAGS} -fPIC|g" Makefile-libbz2_so

	patch -Np1 -i $srcdir/bzip2-1.0.4-bzip2recover.patch
	patch -Np1 -i $srcdir/bzip2-1.0.6-CVE-2016-3189.patch
}

build() {
	cd $pkgname-$pkgver
	make
	make -f Makefile-libbz2_so
}

package() {
	cd $pkgname-$pkgver
	install -dm755 $pkgdir/usr/{bin,lib,include,share/man/man1}

	install -m755 bzip2-shared $pkgdir/usr/bin/bzip2
	install -m755 bzip2recover bzdiff bzgrep bzmore $pkgdir/usr/bin
	ln -sf bzip2 $pkgdir/usr/bin/bunzip2
	ln -sf bzip2 $pkgdir/usr/bin/bzcat

	install -m755 libbz2.so.1.0.6 $pkgdir/usr/lib
	ln -s libbz2.so.1.0.6 $pkgdir/usr/lib/libbz2.so
	ln -s libbz2.so.1.0.6 $pkgdir/usr/lib/libbz2.so.1
	ln -s libbz2.so.1.0.6 $pkgdir/usr/lib/libbz2.so.1.0

	install -m644 bzlib.h $pkgdir/usr/include/

	install -m644 bzip2.1 $pkgdir/usr/share/man/man1/
	ln -sf bzip2.1 $pkgdir/usr/share/man/man1/bunzip2.1
	ln -sf bzip2.1 $pkgdir/usr/share/man/man1/bzcat.1
	ln -sf bzip2.1 $pkgdir/usr/share/man/man1/bzip2recover.1
}
