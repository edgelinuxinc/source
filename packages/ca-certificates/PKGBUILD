# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=ca-certificates
pkgver=20180409
pkgrel=1
pkgdesc="Common CA certificates (default providers)"
arch=('x86_64' 'aarch64')
url="https://minitena.github.io/"
license=('custom')
depends=('glibc' 'zlib' 'libressl')
source=("https://github.com/protonesso/yoctus-baselayout/raw/master/ca-certificates_20180409.tar.xz"
	'certdata2pem.c'
	'c_rehash.c'
	'update-ca-certificates.hook')
md5sums=('8425f4b1219e4e528db2de764e4c6586'
         '1c759bac40fcae93119696ae24144ee2'
         'eba0f9b8d993f21c900a12ca03301344'
         '7b5e5674c33848c1de749f02e10fd8d1')

prepare() {
	cd "$srcdir/$pkgname"
	sed -e 's#openssl rehash#c_rehash#' -i update-ca-certificates
}

build() {
	cd "$srcdir/$pkgname"

cat > sbin/Makefile << EOF
install:
	install -Dm755 update-ca-certificates \$(DESTDIR)\$(SBINDIR)/update-ca-certificates
EOF

mv $srcdir/certdata2pem.c mozilla/certdata2pem.c

cat > mozilla/Makefile << EOF
all:
	gcc -Wall certdata2pem.c -o certdata2pem
	./certdata2pem
clean:
	-rm -f *.crt
install:
	for p in *.crt; do \
	    install -Dm0644 \$\$p \$(CERTSDIR)/\$\$p ; \
	done
EOF

	make

	gcc $srcdir/c_rehash.c -o $srcdir/c_rehash -L$ROOTFS/usr/lib -lssl -lcrypto -lz $CFLAGS
}

package() {
	unset MAKEFLAGS
	cd "$srcdir/$pkgname"
	make DESTDIR=$pkgdir SBINDIR=/usr/bin install

	mkdir -p $pkgdir/etc
	find $pkgdir/usr/share/${pkgname}/ -name '*.crt' | \
		rev | cut -d/ -f1-2 | rev | sort > $pkgdir/etc/ca-certificates.conf

	install -Dm0755 $srcdir/c_rehash $pkgdir/usr/bin/c_rehash
	install -Dm644 $srcdir/update-ca-certificates.hook $pkgdir/usr/share/libalpm/hooks/update-ca-certificates.hook
}
