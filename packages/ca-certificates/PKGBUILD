# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=ca-certificates
pkgver=20180924
pkgrel=1
pkgdesc="Common CA certificates PEM files"
arch=('x86_64' 'aarch64')
url="https://www.mozilla.org/en-US/about/governance/policies/security-group/certs/"
license=('GPL' 'MPL')
depends=('glibc' 'libressl')
source=("https://git.alpinelinux.org/cgit/ca-certificates/snapshot/ca-certificates-$pkgver.tar.bz2"
	'ca-certificates.hook')
md5sums=('2574e46b82496b4731971db8a7ad0261'
         'b140d22492bb9a2c655c23b6c1a5ebe1')

build() {
	cd ca-certificates-$pkgver
	make
}

package() {
	cd ca-certificates-$pkgver
	make DESTDIR=$pkgdir install

	(
		cd $pkgdir/usr/share/ca-certificates
		find . -name '*.crt' | sort | cut -b3-
	) > $pkgdir/etc/ca-certificates.conf

	cat > $pkgdir/etc/ca-certificates/update.d/certhash <<-EOF
		#!/bin/sh
		exec /usr/bin/c_rehash /etc/ssl/certs
	EOF
	chmod +x $pkgdir/etc/ca-certificates/update.d/certhash

	install -dm755 $pkgdir/usr/share/libalpm/hooks/
	install -m644 $srcdir/*.hook $pkgdir/usr/share/libalpm/hooks/
}
