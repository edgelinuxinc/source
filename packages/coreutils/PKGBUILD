# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=coreutils
pkgver=8.30
pkgrel=2
pkgdesc="The basic file, shell and text manipulation utilities"
arch=('x86_64' 'i586' 'aarch64')
url="http://www.gnu.org/software/coreutils"
license=('GPL')
depends=('glibc' 'gmp' 'attr' 'acl' 'libcap' 'libselinux')
source=("http://ftpmirror.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz")
md5sums=('ab06d68949758971fe744db66b572816')

build() {
	if [ -f /usr/lib/libselinux.so ]; then
		export SELINUXOPTS="--with-selinux"
	else
		export SELINUXOPTS=''
	fi

	export FORCE_UNSAFE_CONFIGURE=1

	cd $pkgname-$pkgver
	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib \
		--enable-no-install-program=kill,uptime,hostname,groups \
		--build=$CHOST $SELINUXOPTS
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR=$pkgdir install
}
