# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=cracklib
pkgver=2.9.6
pkgrel=8
pkgdesc="Password Checking Library"
arch=('x86_64' 'aarch64')
url="https://github.com/cracklib/cracklib"
license=('GPL')
depends=('glibc' 'zlib')
source=(https://github.com/cracklib/cracklib/releases/download/$pkgname-$pkgver/$pkgname-$pkgver.tar.gz
	https://github.com/cracklib/cracklib/releases/download/$pkgname-$pkgver/cracklib-words-$pkgver.gz
	cracklib.hook)
md5sums=('c52f463585d85924b28cdc1e373ae06d'
         'cae1257e3a9f95c917cc9dbbab852316'
         'f86cfcd8ec03715864d2951ab4ac0e06')

build() {
	cd $pkgname-$pkgver
	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--with-default-dict=/usr/lib/cracklib/pw_dict \
		--without-python \
		--build=$CHOST
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR=$pkgdir install

	install -D -m644 $srcdir/cracklib-words-$pkgver.gz $pkgdir/usr/share/dict/cracklib-words.gz

	gunzip $pkgdir/usr/share/dict/cracklib-words.gz
	rm -rf $pkgdir/usr/share/dict/cracklib-words.gz

	ln -sf cracklib-words $pkgdir/usr/share/dict/words

	install -m755 -d $pkgdir/usr/lib/cracklib

	install -dm755 $pkgdir/usr/share/libalpm/hooks/
	install -m644 $srcdir/*.hook $pkgdir/usr/share/libalpm/hooks/
}
