# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=cracklib
pkgver=2.9.6
pkgrel=1
pkgdesc="Password Checking Library"
arch=('x86_64' 'aarch64')
url="https://github.com/cracklib/cracklib"
license=('GPL')
depends=('glibc' 'zlib')
source=("https://github.com/cracklib/cracklib/releases/download/$pkgname-$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('c52f463585d85924b28cdc1e373ae06d')

build() {
	cd $pkgname-$pkgver
	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--without-python \
		--build=$CHOST
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR=$pkgdir install

	mkdir -p $pkgdir/usr/share/dict
	ln -sf /usr/share/cracklib/cracklib-small $pkgdir/usr/share/dict/cracklib-small
	sh ./util/cracklib-format dicts/cracklib-small \
		| sh ./util/cracklib-packer $pkgdir/usr/share/cracklib/pw_dict
}
