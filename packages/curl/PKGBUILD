# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=curl
pkgver=7.61.1
pkgrel=3
pkgdesc="An URL retrieval utility and library"
arch=('x86_64' 'i586' 'aarch64')
url="https://curl.haxx.se/"
license=('MIT')
depends=('glibc' 'zlib' 'libressl' 'libidn2' 'libssh2')
source=("https://curl.haxx.se/download/$pkgname-$pkgver.tar.xz")
md5sums=('feb1856fcdb79ba0386c8e41b02e487d')

build() {
	cd $pkgname-$pkgver
	./configure \
		--prefix=/usr \
		--mandir=/usr/share/man \
		--with-ca-bundle=/etc/ssl/cert.pem \
		--with-random=/dev/urandom \
		--with-gssapi \
		--with-libssh2 \
		--enable-ipv6 \
		--enable-threaded-resolver \
		--enable-versioned-symbols \
		--disable-manual \
		--build=$CHOST
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR=$pkgdir install
	make DESTDIR=$pkgdir install -C scripts
}
