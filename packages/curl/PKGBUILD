# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=curl
pkgver=7.63.0
pkgrel=3
pkgdesc="An URL retrieval utility and library"
arch=('x86_64' 'i586' 'aarch64' 'armv7h' 'armv6h' 'armv5tel' 'powerpc64le' 'powerpc64' 'powerpc' 'sparc64' 's390x' 'riscv64')
url="https://curl.haxx.se/"
license=('MIT')
depends=('glibc' 'zlib' 'libressl' 'libidn2' 'libssh2')
source=("https://curl.haxx.se/download/$pkgname-$pkgver.tar.xz")
md5sums=('f43d618cc49c1820d3a2fed31e451d4c')

build() {
	cd $pkgname-$pkgver
	./configure \
		--prefix=/usr \
		--mandir=/usr/share/man \
		--with-ca-bundle=/etc/ssl/cert.pem \
		--with-random=/dev/urandom \
		--with-libssh2 \
		--enable-ipv6 \
		--enable-threaded-resolver \
		--enable-versioned-symbols \
		--disable-ldap \
		--disable-manual \
		--build=$CHOST
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR=$pkgdir install
	make DESTDIR=$pkgdir install -C scripts
}
