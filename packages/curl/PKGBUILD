# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=curl
pkgver=7.62.0
pkgrel=3
pkgdesc="An URL retrieval utility and library"
arch=('x86_64' 'i586' 'aarch64' 'arm' 'mips64' 'mips' 'powerpc64le' 'powerpc64' 'powerpc' 'sparc64' 'sparc' 's390x' 'microblaze' 'riscv64' 'riscv32')
url="https://curl.haxx.se/"
license=('MIT')
depends=('glibc' 'zlib' 'libressl' 'libidn2' 'libssh2')
source=("https://curl.haxx.se/download/$pkgname-$pkgver.tar.xz")
md5sums=('d4613e369a8ff80f23951d259808149e')

build() {
	export CFLAGS="${CFLAGS/-O3/-Os}"
	export CXXFLAGS="${CXXFLAGS/-O3/-Os}"

	cd $pkgname-$pkgver
	./configure \
		--prefix=/usr \
		--mandir=/usr/share/man \
		--with-ca-bundle=/etc/ssl/cert.pem \
		--with-random=/dev/urandom \
		--with-libssh2 \
		--enable-ipv6 \
		--enable-threaded-resolver \
		--enable-versioned-symbols \
		--disable-ldap \
		--disable-manual \
		--build=$CHOST
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR=$pkgdir install
	make DESTDIR=$pkgdir install -C scripts
}
