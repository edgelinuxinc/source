# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=dnsmasq
pkgver=2.80
pkgrel=1
pkgdesc="Lightweight, easy to configure DNS forwarder and DHCP server"
arch=('x86_64' 'i586' 'aarch64' 'arm' 'mips64' 'mips' 'powerpc64le' 'powerpc64' 'powerpc' 'sparc64' 'sparc' 's390x' 'microblaze' 'riscv64' 'riscv32')
url="http://www.thekelleys.org.uk/dnsmasq/doc.html"
license=('GPL')
depends=('glibc' 'gmp' 'libidn2' 'dbus' 'nettle' 'libnetfilter_conntrack')
source=("http://www.thekelleys.org.uk/$pkgname/$pkgname-$pkgver.tar.xz"
	'service')
md5sums=('e040e72e6f377a784493c36f9e788502'
         'bece98b87e1139ce178868ded917cedc')

build() {
	cd $pkgname-$pkgver
	make \
		CFLAGS="$CFLAGS" \
		COPTS="-DHAVE_DNSSEC -DHAVE_DBUS -DHAVE_LIBIDN2 -DHAVE_CONNTRACK" \
		PREFIX=/usr \
		BINDIR=/usr/bin \
		all-i18n
}

package() {
	cd $pkgname-$pkgver
	make \
		COPTS="-DHAVE_DNSSEC -DHAVE_DBUS -DHAVE_LIBIDN2 -DHAVE_CONNTRACK" \
		PREFIX=/usr \
		BINDIR=/usr/bin \
		DESTDIR=$pkgdir \
		install install-i18n

	install -Dm644 dbus/dnsmasq.conf $pkgdir/usr/share/dbus-1/system.d/dnsmasq.conf
	install -Dm644 dnsmasq.conf.example $pkgdir/etc/dnsmasq.conf
	install -Dm644 $srcdir/service $pkgdir/usr/lib/systemd/system/dnsmasq.service

	sed -i 's,%%PREFIX%%,/usr,' $pkgdir/etc/dnsmasq.conf
	install -Dm644 "trust-anchors.conf" $pkgdir/usr/share/dnsmasq/trust-anchors.conf
}
