# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=dracut
pkgver=048
pkgrel=4
pkgdesc="Generic, modular, cross-distribution initramfs generation tool"
arch=('x86_64' 'i586' 'aarch64' 'arm' 'riscv32' 'riscv64')
url="https://dracut.wiki.kernel.org/"
license=('GPL')
depends=('systemd' 'util-linux' 'bash' 'kbd' 'kmod' 'cpio')
makedepends=('docbook-xsl' 'asciidoc')
backup=('etc/dracut.conf')
source=("http://www.kernel.org/pub/linux/utils/boot/$pkgname/$pkgname-$pkgver.tar.xz")
md5sums=('a9705d3c32c7523428f6e5e3e17244f8')

prepare() {
	cd $pkgname-$pkgver
	sed -i 's|$(arch)|$(uname -m)|g' modules.d/90crypt/module-setup.sh
}

build() {
	cd $pkgname-$pkgver
	./configure \
		--prefix=/usr --bindir=/usr/bin --sbindir=/usr/bin \
		--libdir=/usr/lib --libexecdir=/usr/lib/dracut \
		--systemdsystemunitdir=/usr/lib/systemd/system \
		--bashcompletiondir=/usr/share/bash-completion/completions \
		--sysconfdir=/etc --loginstalldir=/var/log/dracut
	make \
		sysconfdir=/etc prefix=/usr \
		bindir=/usr/bin sbindir=/usr/bin \
		libdir=/usr/lib libexecdir=/usr/lib/dracut \
		systemdsystemunitdir=/usr/lib/systemd/system \
		bashcompletiondir=/usr/share/bash-completion/completions \
		sysconfdir=/etc loginstall=/var/log/dracut
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR=$pkgdir \
		sysconfdir=/etc prefix=/usr \
		bindir=/usr/bin sbindir=/usr/bin \
		libdir=/usr/lib libexecdir=/usr/lib/dracut \
		systemdsystemunitdir=/usr/lib/systemd/system \
		bashcompletiondir=/usr/share/bash-completion/completions \
		sysconfdir=/etc loginstall=/var/log/dracut install
}
