# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=e2fsprogs
pkgver=1.44.5
pkgrel=1
pkgdesc="Ext2/3/4 filesystem utilities"
arch=('x86_64' 'i586' 'aarch64' 'armv7h' 'armv6h' 'armv5tel' 'powerpc64le' 'powerpc64' 'powerpc' 'sparc64' 's390x' 'riscv64')
url="http://e2fsprogs.sourceforge.net"
license=('GPL' 'LGPL' 'MIT')
depends=('glibc' 'util-linux-libs')
makedepends=('util-linux')
options=('staticlibs')
backup=('etc/mke2fs.conf')
source=("https://downloads.sourceforge.net/project/$pkgname/$pkgname/v$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('8d78b11d04d26c0b2dd149529441fa80')

build() {
	cd $pkgname-$pkgver
	./configure \
		--prefix=/usr \
		--with-root-prefix="" \
		--libdir=/usr/lib \
		--sbindir=/usr/bin \
		--enable-elf-shlibs \
		--disable-fsck \
		--disable-libblkid \
		--disable-libuuid \
		--disable-uuidd \
		--build=$CHOST
	make
}

package() {
	unset MAKEFLAGS

	cd $pkgname-$pkgver
	make DESTDIR=$pkgdir install
	make DESTDIR=$pkgdir install-libs

	sed -i -e 's#^SS_DIR=.*#SS_DIR="/usr/share/ss"#' $pkgdir/usr/bin/mk_cmds
	sed -i -e 's#^ET_DIR=.*#ET_DIR="/usr/share/et"#' $pkgdir/usr/bin/compile_et

	rm $pkgdir/usr/lib/lib{com_err,e2p,ext2fs,ss}.a
}
