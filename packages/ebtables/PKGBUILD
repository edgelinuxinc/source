# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=ebtables
pkgver=2.0.10_4
pkgrel=1
pkgdesc="Ethernet bridge filtering utilities"
arch=('x86_64' 'i586' 'aarch64' 'arm')
url="http://ebtables.sourceforge.net/"
license=('GPL')
depends=('glibc' 'iptables' 'perl')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-v${pkgver/_/-}.tar.gz"
	'systemd'
	'service')
md5sums=('506742a3d44b9925955425a659c1a8d0'
         'b4c329060809e5b290ae49fbd0ad61f2'
         '5fc546b232c8e4d66058031ddde71b2f')

build() {
	cd $pkgname-v${pkgver/_/-}
	make CFLAGS="$CFLAGS -Wunused -Wall -Wno-error=unused-but-set-variable"
}

package() {
	cd $pkgname-v${pkgver/_/-}
	make install \
		DESTDIR=$pkgdir \
		LIBDIR=/usr/lib \
		MANDIR=/usr/share/man \
		BINDIR=/usr/bin \
		INITDIR=/etc/rc.d \
		SYSCONFIGDIR=/etc

	rm $pkgdir/etc/ebtables-config
	rm -r $pkgdir/etc/rc.d

	install -Dm 755 $srcdir/systemd $pkgdir/usr/lib/systemd/scripts/$pkgname
	install -Dm 644 $srcdir/service $pkgdir/usr/lib/systemd/system/$pkgname.service

	install -Dm 644 /dev/null $pkgdir/etc/$pkgname.conf

	rm $pkgdir/etc/ethertypes
}
