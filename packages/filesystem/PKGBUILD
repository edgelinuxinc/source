# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=filesystem
pkgver=2018.09
pkgrel=2
pkgdesc="Filesystem layout and essential files"
arch=('x86_64' 'aarch64')
url="https://minitena.github.io/"
license=('MIT')
source=(env-gen
	fstab
	group
	gshadow
	host.conf
	hosts
	issue
	ld.so.conf
	motd
	nsswitch.conf
	os-release
	passwd
	profile
	protocols
	resolv.conf
	securetty
	services
	shadow
	shells
	sysctl
	enter-chroot
	genfstab
	pacstrap
	zzz)
md5sums=('2b0344e9639f35f3c0d5637a23556089'
         'e33f6dfdd61978fcb3ddf1431286e05a'
         'a3ee055b9aad9bcfb22539f527114b9a'
         '2f5a0f0f17dbca37c33e78fde9b0b0c9'
         'a61b9f6548d337c1cc1e5a4de39f7b7f'
         '0166671b0bfc53018c8d7a6ec53dfcb0'
         '01c1dddff3bcb144c5bb77812e481a90'
         '2c21aad665524d62c8fa27e02a652618'
         'd41d8cd98f00b204e9800998ecf8427e'
         '12dcf2806cae99033e093387eb255a15'
         '6ee851f8ddf7099e90987bedc328e0a8'
         '36dbb9a9dd7f878d8a27476076023d17'
         '4500b3a486055cd302dd0e87d8af348f'
         'd46c8af3927f3c5f748e359871bdf236'
         'fe0b9046ae1a0ee676446c08ef4f2af7'
         '49c1d411ff33b6df4d55a6a6f9a9bf92'
         '9d39e51e6bd5a30168c26c57b66d2e37'
         '028557ba06b85fb6bd829afead4d7747'
         'bdc193807f1f76f8aacd492f5a5b575e'
         '03f456264db977631ac80106d990b2a7'
         '0ee4a4339934a190b36f1bc6ea8dbaa3'
         '7add791064fdee57272ead354819b820'
         'b51958396adece663ddf7d1aa4bb8bcb'
         'b6bfe108600d3985dd2d8ba0090fe5f7')
backup=('etc/fstab'
	'etc/group'
	'etc/gshadow'
	'etc/host.conf'
	'etc/hosts'
	'etc/issue'
	'etc/ld.so.conf'
	'etc/motd'
	'etc/nsswitch.conf'
	'etc/passwd'
	'etc/profile'
	'etc/resolv.conf'
	'etc/securetty'
	'etc/shadow'
	'etc/shells')

package() {
	cd $pkgdir
	for d in boot dev etc/{ld.so.conf.d,skel,profile.d,modprobe.d,sysctl.d} home mnt usr var opt srv/http run; do
		install -d -m755 $d
	done

	install -d -m555 proc
	install -d -m555 sys
	install -d -m0750 root
	install -d -m1777 tmp
	install -d -m555 -g 11 srv/ftp

	for f in fstab group gshadow host.conf hosts issue ld.so.conf motd nsswitch.conf os-release passwd profile protocols resolv.conf securetty services shadow shells; do
		install -m644 $srcdir/$f $pkgdir/etc
	done

	chmod 0600 $pkgdir/etc/gshadow
	chmod 0600 $pkgdir/etc/shadow

	ln -sf ../proc/self/mounts etc/mtab

	for d in cache local opt log/old lib/misc empty; do
		install -d -m755 var/$d
	done

	install -d -m1777 var/{tmp,spool/mail}

	install -d -m775 -g 50 var/games
	ln -s spool/mail var/mail
	ln -s ../run var/run
	ln -s ../run/lock var/lock

	for d in bin include lib/{modules,firmware} share/misc; do
		install -d -m755 usr/$d
	done

	ln -s usr/lib lib
	case $BARCH in
		x86_64|aarch64)
			ln -s usr/lib lib64
			ln -s lib usr/lib64
			;;
	esac

	ln -s usr/bin bin
	ln -s usr/bin sbin
	ln -s bin usr/sbin

	for d in bin etc games include lib man sbin share; do
		install -d -m755 usr/local/$d
	done

	install -D -m755 $srcdir/env-gen $pkgdir/usr/lib/systemd/system-environment-generators/10-default
	install -D -m644 $srcdir/sysctl $pkgdir/usr/lib/sysctl.d/10-default.conf

	install -D -m755 $srcdir/enter-chroot $pkgdir/usr/bin/enter-chroot
	install -D -m755 $srcdir/genfstab $pkgdir/usr/bin/genfstab
	install -D -m755 $srcdir/pacstrap $pkgdir/usr/bin/pacstrap
	install -D -m755 $srcdir/zzz $pkgdir/usr/bin/zzz
}