# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=filesystem
pkgver=2018.11
pkgrel=8
pkgdesc="Filesystem layout and essential files"
arch=('x86_64' 'i586' 'aarch64' 'arm' 'mips64' 'mips' 'powerpc64le' 'powerpc64' 'powerpc' 'sparc64' 'sparc' 's390x' 'microblaze' 'riscv64' 'riscv32')
url="https://minitena.github.io/"
license=('MIT')
backup=('etc/fstab'
	'etc/group'
	'etc/gshadow'
	'etc/host.conf'
	'etc/hosts'
	'etc/issue'
	'etc/ld.so.conf'
	'etc/locale.conf'
	'etc/motd'
	'etc/nsswitch.conf'
	'etc/passwd'
	'etc/profile'
	'etc/resolv.conf'
	'etc/securetty'
	'etc/shadow'
	'etc/shells'
	'etc/modprobe.d/blacklist.conf')
source=(env-gen
	fstab
	group
	gshadow
	host.conf
	hosts
	issue
	ld.so.conf
	locale.conf
	locale.sh
	motd
	nsswitch.conf
	os-release
	passwd
	profile
	protocols
	resolv.conf
	securetty
	services
	shadow
	shells
	sysctl
	tmpfiles
	enter-chroot
	genfstab
	pacstrap
	blacklist.conf)
md5sums=('2b0344e9639f35f3c0d5637a23556089'
         'e33f6dfdd61978fcb3ddf1431286e05a'
         '0923359259f58038ee1515a9c84fbc14'
         '265dac4a1ad0f636cc53bdd026af50de'
         'a61b9f6548d337c1cc1e5a4de39f7b7f'
         '0166671b0bfc53018c8d7a6ec53dfcb0'
         '10f47912b22bd58765813b7b78b5b5f2'
         '2c21aad665524d62c8fa27e02a652618'
         'e24819fd5acc025b5b307b77bb871f65'
         '580cf8b59eebfaac4fad373ffadc9912'
         'd41d8cd98f00b204e9800998ecf8427e'
         '12dcf2806cae99033e093387eb255a15'
         '20485dc1677b63663048544f774ce23f'
         'c1fbd4d60d9ff66d1f8fe2951dda9973'
         '4500b3a486055cd302dd0e87d8af348f'
         'd46c8af3927f3c5f748e359871bdf236'
         'fe0b9046ae1a0ee676446c08ef4f2af7'
         '49c1d411ff33b6df4d55a6a6f9a9bf92'
         '9d39e51e6bd5a30168c26c57b66d2e37'
         '3821106936dfd350037ca08d97a37217'
         'bdc193807f1f76f8aacd492f5a5b575e'
         '3678409d55c834d709ffe356a6b3cfc5'
         '4d49ad29e0a1cd0b8e41ee4743a5f085'
         '0ee4a4339934a190b36f1bc6ea8dbaa3'
         '7add791064fdee57272ead354819b820'
         'b51958396adece663ddf7d1aa4bb8bcb'
         'a8ff6379f3fc9437c57788f1d0e54945')

package() {
	cd $pkgdir
	for d in boot dev etc/{ld.so.conf.d,skel,profile.d,modprobe.d,sysctl.d} home mnt usr/share/factory/etc var opt srv/http run; do
		install -d -m755 $d
	done

	install -d -m555 proc
	install -d -m555 sys
	install -d -m0750 root
	install -d -m1777 tmp
	install -d -m555 -g 11 srv/ftp

	for f in fstab group gshadow host.conf hosts issue ld.so.conf locale.conf motd nsswitch.conf os-release passwd profile protocols resolv.conf securetty services shadow shells; do
		install -m644 $srcdir/$f $pkgdir/etc
		install -m644 $srcdir/$f $pkgdir/usr/share/factory/etc
	done

	chmod 0600 $pkgdir/etc/gshadow
	chmod 0600 $pkgdir/etc/shadow

	install -m644 $srcdir/blacklist.conf $pkgdir/etc/modprobe.d/

	ln -sf ../proc/self/mounts etc/mtab

	for d in cache local opt log/old lib/misc empty; do
		install -d -m755 var/$d
	done

	install -d -m1777 var/{tmp,spool/mail}

	install -d -m775 -g 50 var/games
	ln -sf spool/mail var/mail
	ln -sf ../run var/run
	ln -sf ../run/lock var/lock

	for d in bin include lib/{modules,firmware} share/misc; do
		install -d -m755 usr/$d
	done

	ln -sf usr/lib lib
	case $CARCH in
		x86_64|aarch64|mips64|powerpc64le|powerpc64|sparc64|s390x|riscv64)
			ln -sf usr/lib lib64
			ln -sf lib usr/lib64
			;;
	esac

	ln -sf usr/bin bin
	ln -sf usr/bin sbin
	ln -sf bin usr/sbin

	for d in bin etc games include lib man sbin share; do
		install -d -m755 usr/local/$d
	done

	install -D -m755 $srcdir/env-gen $pkgdir/usr/lib/systemd/system-environment-generators/10-default
	install -D -m644 $srcdir/tmpfiles $pkgdir/usr/lib/tmpfiles.d/10-default.conf
	install -D -m644 $srcdir/sysctl $pkgdir/usr/lib/sysctl.d/10-default.conf

	install -m755 $srcdir/locale.sh $pkgdir/etc/profile.d/locale.sh

	install -D -m755 $srcdir/enter-chroot $pkgdir/usr/bin/enter-chroot
	install -D -m755 $srcdir/genfstab $pkgdir/usr/bin/genfstab
	install -D -m755 $srcdir/pacstrap $pkgdir/usr/bin/pacstrap
}
