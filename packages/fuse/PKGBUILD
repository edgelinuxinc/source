# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=fuse
pkgver=3.2.6
pkgrel=2
pkgdesc="A library that makes it possible to implement a filesystem in a userspace program"
arch=('x86_64' 'i586' 'aarch64' 'arm' 'riscv64' 'riscv32')
url="https://github.com/libfuse/libfuse"
license=('GPL')
backup=('etc/fuse.conf')
depends=('glibc')
makedepends=('meson')
options=(!emptydirs)
source=("https://github.com/libfuse/libfuse/releases/download/$pkgname-$pkgver/$pkgname-$pkgver.tar.xz"
	'fuse.conf')
md5sums=('4b068c444a4d2e62f61ff205363e2757'
         'c9457cf5b2196da67d5ac816d1c86a4f')

prepare() {
	cd $pkgname-$pkgver
	mkdir build
}

build() {
	cd $pkgname-$pkgver
	cd build 

	meson $srcdir/$pkgname-$pkgver \
		--prefix=/usr \
		--sbindir=/usr/bin
	ninja
}

package() {
	cd $pkgname-$pkgver
	cd build

	DESTDIR=$pkgdir ninja install

	install -Dm644 $srcdir/fuse.conf $pkgdir/etc/fuse.conf

	rm -r ${pkgdir}/etc/init.d
	rm -r ${pkgdir}/dev
}
