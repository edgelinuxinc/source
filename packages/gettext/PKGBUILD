# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=gettext
pkgver=0.19.8.1
pkgrel=1
pkgdesc="The GNU internationalization library"
arch=('x86_64' 'aarch64')
url="http://www.gnu.org/software/gettext"
license=('GPL')
depends=('glibc' 'gcc-libs' 'acl')
source=("http://ftpmirror.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz")
md5sums=('df3f5690eaa30fd228537b00cb7b7590')

prepare() {
	cd $pkgname-$pkgver
	sed -e '/AppData/{N;N;p;s/\.appdata\./.metainfo./}' \
		-i gettext-tools/its/appdata.loc
}

build() {
	cd $pkgname-$pkgver
	./configure \
		--prefix=/usr \
		--build=$CHOST
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR=$pkgdir install

	chmod 0755 $pkgdir/usr/lib/preloadable_libintl.so
}
