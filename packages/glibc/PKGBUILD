# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=glibc
pkgver=2.28
pkgrel=9
pkgdesc="The GNU C Library"
arch=('x86_64' 'i586' 'aarch64' 'arm' 'mips64' 'mips' 'powerpc64le' 'powerpc64' 'powerpc' 'sparc64' 'sparc' 's390x' 'microblaze' 'riscv64' 'riscv32')
url="http://www.gnu.org/software/libc"
license=('GPL' 'LGPL')
depends=('filesystem' 'tzdata' 'linux-headers')
backup=(etc/gai.conf
	etc/nscd.conf)
options=('docs' 'staticlibs')
install=$pkgname.install
source=("http://ftpmirror.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz")
md5sums=('c81d2388896379997bc359d4f2084239')

prepare() {
	cd $pkgname-$pkgver
	mkdir build
	cd build

	echo "slibdir=/usr/lib" >> configparms
	echo "rtlddir=/usr/lib" >> configparms
	echo "sbindir=/usr/bin" >> configparms
	echo "rootsbindir=/usr/bin" >> configparms
}

build() {
	cd $pkgname-$pkgver
	cd build
	../configure \
		--prefix=/usr \
		--libdir=/usr/lib \
		--libexecdir=/usr/lib \
		--with-pkgversion="Minitena $pkgver" \
		--with-bugurl='https://github.com/minitena/source/issues' \
		--without-gd \
		--without-selinux \
		--enable-add-ons \
		--enable-bind-now \
		--enable-kernel=3.12.0 \
		--enable-lock-elision \
		--enable-stack-protector=strong \
		--enable-stackguard-randomization \
		--disable-profile \
		--disable-werror \
		--build=$CHOST $GLIBCOPTS
	make
}

package() {
	cd $pkgname-$pkgver
	cd build

	install -dm755 $pkgdir/etc
	touch $pkgdir/etc/ld.so.conf

	make install_root=$pkgdir install

	rm -rf $pkgdir/etc/ld.so.{cache,conf}

	rm -rf $pkgdir/usr/bin/{tzselect,zdump,zic}

	install -dm755 $pkgdir/usr/lib/{locale,systemd/system,tmpfiles.d}
	install -m644 ../nscd/nscd.conf $pkgdir/etc/nscd.conf
	install -m644 ../nscd/nscd.service $pkgdir/usr/lib/systemd/system
	install -m644 ../nscd/nscd.tmpfiles $pkgdir/usr/lib/tmpfiles.d/nscd.conf
	install -dm755 $pkgdir/var/db/nscd

	install -m644 ../posix/gai.conf $pkgdir/etc/gai.conf

	make SUPPORTED-LOCALES="en_US.UTF-8/UTF-8" install_root=$pkgdir localedata/install-locales

	cd $pkgdir
	rm -rf {,usr/}{,local/}{,share/}{doc,man,info,locale} {,usr/}{,local/}{,share/},opt/*}/{man,info} {,usr/}{,local/}{,share/}{doc,gtk-doc} opt/*/{doc,gtk-doc}
}
