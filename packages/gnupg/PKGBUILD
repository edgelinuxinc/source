# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=gnupg
pkgver=2.2.11
pkgrel=1
pkgdesc="Complete and free implementation of the OpenPGP standard"
arch=('x86_64' 'i586' 'aarch64' 'arm')
url="https://www.gnupg.org/"
license=('GPL')
depends=('glibc' 'bzip2' 'readline' 'sqlite' 
	'libgpg-error' 'libgcrypt' 'libassuan' 'libksba' 'npth' 'pinentry')
source=("https://www.gnupg.org/ftp/gcrypt/$pkgname/$pkgname-$pkgver.tar.bz2")
md5sums=('e23a896d634e8b81681314780f5158a4')

prepare() {
	cd $pkgname-$pkgver
	sed -e '/noinst_SCRIPTS = gpg-zip/c sbin_SCRIPTS += gpg-zip' -i tools/Makefile.in
}

build() {
	cd $pkgname-$pkgver
	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib/gnupg \
		--sbindir=/usr/bin \
		--sysconfdir=/etc \
		--enable-g13 \
		--enable-symcryptrun \
		--build=$CHOST
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR=$pkgdir install

	cd doc/examples/systemd-user
	for i in *.*; do
		install -Dm644 $i $pkgdir/usr/lib/systemd/user/$i
	done
}
