# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=go
pkgver=1.11.2
pkgrel=1
pkgdesc="Core compiler tools for the Go programming language"
arch=('x86_64' 'i586' 'aarch64' 'arm')
url="http://golang.org/"
license=('BSD')
depends=('binutils')
makedepends=('gcc')
options=(!strip staticlibs)
source=("https://storage.googleapis.com/golang/go$pkgver.src.tar.gz"
	'default-buildmode-pie.patch')
md5sums=('0b808590b1b10f1072b5f52105d8e285'
         '43e72bcddf31081ffc2aa7ef5e48c740')

prepare() {
	cd $pkgname
	patch -p1 -i $srcdir/default-buildmode-pie.patch
}

build() {
	cd $pkgname/src
	case $CARCH in
		x86_64)
			export GARCH="amd64"
			;;
		i586)
			export GARCH="386"
			;;
		aarch64)
			export GARCH="arm64"
			;;
		arm)
			export GARCH="arm"
			;;
		*)
			echo "Architecture is not set or is not supported"
			exit 1
	esac

	export GOCACHE=off
	export GOROOT="$(pwd)"
	export GOROOT_FINAL="/usr/lib/go"
	export GOROOT_BOOTSTRAP="/usr/lib/gccgo/"
	export GOARCH="$GARCH"
	export GOOS="linux"

	bash make.bash -v
}

package() {
	cd $pkgname

	install -d $pkgdir/usr/bin $pkgdir/usr/lib/go
	cp -a bin pkg src lib misc api $pkgdir/usr/lib/go

	ln -sf /usr/lib/go/bin/go $pkgdir/usr/bin/go
	ln -sf /usr/lib/go/bin/gofmt $pkgdir/usr/bin/gofmt

	install -Dm644 VERSION $pkgdir/usr/lib/go/VERSION

	rm -rf $pkgdir/usr/lib/go/pkg/bootstrap
	rm -rf $pkgdir/usr/lib/go/pkg/tool/*/api

	rm -rf $pkgdir/usr/lib/go/pkg/obj/go-build/*
}
