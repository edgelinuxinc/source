# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=grub
pkgver=2.02
pkgrel=1
pkgdesc="The GNU GRand Unified Bootloader"
arch=('x86_64' 'i586' 'aarch64')
url="http://www.gnu.org/software/grub"
license=('GPL')
depends=('glibc' 'xz-utils' 'lvm2')
makedepends=('xz-utils' 'python2' 'gnu-efi' 'lvm2')
backup=('boot/grub/grub.cfg'
	'etc/default/grub'
	'etc/grub.d/40_custom')
options=('!makeflags')
source=("http://ftpmirror.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz"
	'0004-add-GRUB_COLOR_variables.patch'
	'0005-Allow_GRUB_to_mount_ext234_filesystems_that_have_the_encryption_feature.patch'
	'0006-tsc-Change-default-tsc-calibration-method-to-pmtimer-on-EFI-systems.patch'
	'0007-grub-mkconfig_10_linux_Support_multiple_early_initrd_images.patch'
	'0008-Fix-packed-not-aligned-error-on-GCC-8.patch'
	'0009-xfs-Accept-filesystem-with-sparse-inodes.patch'
	'0010-relocation.patch'
	'grub.default')
md5sums=('8a4a2a95aac551fb0fba860ceabfa1d3'
         'e506ae4a9f9f7d1b765febfa84e10d48'
         '7d4aa743cfabe49158e970a97f7795f0'
         '0b459ce31fecd7f61d373fd064f15dcc'
         '2256273cdf34e1a7a0976bb0b6e6c1de'
         '6b7813c4c3cb2704939782430f0314cf'
         '1801dc136206eb7b5bb0592486b82c47'
         'd1ae83c2c43709535c60e5f30d7aab44'
         '83d3bc77e07bdaac126fffc1f04da2a4')

prepare() {
	cd $pkgname-$pkgver
	patch -Np1 -i $srcdir/0004-add-GRUB_COLOR_variables.patch
	patch -Np1 -i $srcdir/0005-Allow_GRUB_to_mount_ext234_filesystems_that_have_the_encryption_feature.patch
	patch -Np1 -i $srcdir/0006-tsc-Change-default-tsc-calibration-method-to-pmtimer-on-EFI-systems.patch
	patch -Np1 -i $srcdir/0007-grub-mkconfig_10_linux_Support_multiple_early_initrd_images.patch
	patch -Np1 -i $srcdir/0008-Fix-packed-not-aligned-error-on-GCC-8.patch
	patch -Np1 -i $srcdir/0009-xfs-Accept-filesystem-with-sparse-inodes.patch
	patch -Np1 -i $srcdir/0010-relocation.patch

	sed 's|/usr/share/fonts/dejavu|/usr/share/fonts/dejavu /usr/share/fonts/TTF|g' -i "configure.ac"
	sed 's|GNU/Linux||' -i "util/grub.d/10_linux.in"

	./autogen.sh
}

build() {
	cd $pkgname-$pkgver
	unset CFLAGS
	unset CPPFLAGS
	unset CXXFLAGS
	unset LDFLAGS
	unset MAKEFLAGS

	./configure \
		--prefix=/usr \
		--bindir=/usr/bin \
		--sbindir=/usr/bin \
		--mandir=/usr/share/man \
		--infodir=/usr/share/info \
		--datarootdir=/usr/share \
		--sysconfdir=/etc \
		--program-prefix='' \
		--with-bootdir=/boot \
		--with-grubdir=grub \
		--enable-boot-time \
		--enable-device-mapper \
		--disable-werror \
		--build=$CHOST
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR=$pkgdir bashcompletiondir=/usr/share/bash-completion/completions install

	rm -f $pkgdir/usr/lib/grub/*-efi/*.module || true
	rm -f $pkgdir/usr/lib/grub/*-efi/*.image || true
	rm -f $pkgdir/usr/lib/grub/*-efi/{kernel.exec,gdb_grub,gmodule.pl} || true
	rm -f $pkgdir/usr/lib/grub/*-emu/*.module || true
	rm -f $pkgdir/usr/lib/grub/*-emu/*.image || true
	rm -f $pkgdir/usr/lib/grub/*-emu/{kernel.exec,gdb_grub,gmodule.pl} || true
	rm -f $pkgdir/usr/lib/grub/*-pc/*.module || true
	rm -f $pkgdir/usr/lib/grub/*-pc/*.image || true
	rm -f $pkgdir/usr/lib/grub/*-pc/{kernel.exec,gdb_grub,gmodule.pl} || true

	install -D -m0644 $srcdir/grub.default $pkgdir/etc/default/grub
}
