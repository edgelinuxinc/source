# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=guile
pkgver=2.2.4
pkgrel=2
pkgdesc="Portable, embeddable Scheme implementation written in C"
arch=('x86_64' 'i586' 'aarch64' 'armv7h' 'armv6h' 'armv5tel' 'powerpc64le' 'powerpc64' 'powerpc' 'sparc64' 's390x' 'riscv64')
url="https://www.gnu.org/software/guile/"
license=('GPL')
depends=('glibc' 'gmp' 'ncurses' 'libunistring' 'libffi' 'gc')
source=("http://ftpmirror.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('217c7606a0d47cf0b2594852c1f23c02')

build() {
	cd $pkgname-$pkgver
	./configure \
		--prefix=/usr \
		--with-threads \
		--disable-error-on-warning \
		--disable-rpath \
		--build=$CHOST
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR=$pkgdir install

	rm $pkgdir/usr/lib/libguile-2.?.so.*-gdb.scm
}
