# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=htop
pkgver=2.2.0
pkgrel=1
pkgdesc="Interactive process viewer"
arch=('x86_64' 'i586' 'aarch64' 'arm' 'mips' 'mipsel' 'powerpc64le' 'powerpc64' 'powerpc' 'sparc64' 'sparc' 's390x' 'microblaze' 'or1k' 'hppa' 'riscv64' 'riscv32')
url="http://hisham.hm/htop/"
license=('GPL')
depends=('glibc' 'ncurses' 'libnl')
makedepends=('python2')
source=("https://hisham.hm/$pkgname/releases/$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('0d816b6beed31edc75babcfbf863ffa8')

build() {
	cd $pkgname-$pkgver
	PYTHON=/usr/bin/python2 \
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--enable-cgroup \
		--enable-delayacct \
		--enable-linux-affinity \
		--enable-openvz \
		--enable-taskstats \
		--enable-unicode \
		--enable-vserver \
		--disable-static \
		--build=$CHOST
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR=$pkgdir install

	ln -sf htop $pkgdir/usr/bin/top
}
