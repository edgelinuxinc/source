# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=inetutils
pkgver=1.9.4
pkgrel=2
pkgdesc="A collection of common network programs"
arch=('x86_64' 'i586' 'aarch64')
url="http://www.gnu.org/software/inetuils"
license=('GPL')
depends=('glibc' 'libcap' 'readline')
source=(http://ftpmirror.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz
	inetd.service
	ftpd.service
	talk.service
	talk.socket
	telnet.service
	telnet.socket)
md5sums=('87fef1fa3f603aef11c41dcc097af75e'
         '79256bf3f29f2019805ad714f04a6425'
         'fe181e22d3544456f52e64f3bdf5f291'
         '2b7ea36e13178c7a3b3c9b38ac7eb588'
         'bd581fa64b91ded853317334b1489346'
         'd7e5d8f70b7fc7321ab0e61178829b06'
         'bafd84474b3a0942307571e742dd096f')

build() {
	cd $pkgname-$pkgver
	./configure \
		--prefix=/usr \
		--libexec=/usr/bin \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--disable-logger \
		--disable-rcp \
		--disable-rcpd \
		--disable-rexec \
		--disable-rexecd \
		--disable-rlogin \
		--disable-rlogind \
		--disable-rsh \
		--disable-rshd \
		--disable-whois \
		--build=$CHOST
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR=$pkgdir install

	install -D -m644 $srcdir/inetd.service $pkgdir/usr/lib/systemd/system/inetd.service
	install -D -m644 $srcdir/ftpd.service $pkgdir/usr/lib/systemd/system/ftpd.service
	install -D -m644 $srcdir/talk.service $pkgdir/usr/lib/systemd/system/talk.service
	install -D -m644 $srcdir/talk.socket $pkgdir/usr/lib/systemd/system/talk.socket
	install -D -m644 $srcdir/telnet.service $pkgdir/usr/lib/systemd/system/telnet@.service
	install -D -m644 $srcdir/telnet.socket $pkgdir/usr/lib/systemd/system/telnet.socket

	rm -rf $pkgdir/usr/bin/{dnsdomainname,hostname,ifconfig,ping,ping6}
}
