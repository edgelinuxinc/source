# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=iptables
pkgver=1.8.2
pkgrel=5
pkgdesc="Linux kernel packet control tool"
arch=('x86_64' 'i586' 'aarch64' 'arm' 'mips64' 'mips' 'powerpc64le' 'powerpc64' 'powerpc' 'sparc64' 'sparc' 's390x' 'microblaze' 'riscv64' 'riscv32')
url="http://www.netfilter.org/projects/iptables/index.html"
license=('GPL')
depends=('glibc' 'libnftnl')
source=(http://www.netfilter.org/projects/$pkgname/files/$pkgname-$pkgver.tar.bz2
	'iptables-flush'
	'iptables.service'
	'ip6tables.service'
	'example')
md5sums=('944558e88ddcc3b9b0d9550070fa3599'
         'ac9619d7bfa1d9157b8cd873b93a1d79'
         '831be13c245592e93a538b74a89a12bc'
         '2cce244b70ad60dae86947001ca1c84e'
         '88fe330e810bb89b3aa08747bae3e3f9')

prepare() {
	cd $pkgname-$pkgver
	rm include/linux/types.h

	sed -i -e '/libebt_/s/^/#/' \
		-e '/libarpt_/s/^/#/' extensions/GNUmakefile.in
}

build() {
	cd $pkgname-$pkgver
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--sbindir=/usr/bin \
		--libexecdir=/usr/lib/iptables \
		--with-xtlibdir=/usr/lib/xtables \
		--enable-libipq \
		--build=$CHOST
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR=$pkgdir install

	install -d $pkgdir/var/lib/{iptables,ip6tables}

	install -Dm644 $srcdir/example $pkgdir/etc/iptables/iptables.rules

	install -Dm644 $srcdir/iptables.service $pkgdir/usr/lib/systemd/system/iptables.service
	install -Dm644 $srcdir/ip6tables.service $pkgdir/usr/lib/systemd/system/ip6tables.service
	install -Dm755 $srcdir/iptables-flush $pkgdir/usr/lib/systemd/scripts/iptables-flush
}
