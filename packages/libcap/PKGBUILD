# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=libcap
pkgver=2.26
pkgrel=2
pkgdesc="POSIX 1003.1e capabilities"
arch=('x86_64' 'i586' 'aarch64' 'armv7h' 'armv6h' 'armv5tel' 'powerpc64le' 'powerpc64' 'powerpc' 'sparc64' 's390x' 'riscv64')
url="http://sites.google.com/site/fullycapable/"
license=('GPL')
depends=('glibc' 'pam' 'attr')
backup=('etc/security/capability.conf')
source=("https://www.kernel.org/pub/linux/libs/security/linux-privs/libcap2/$pkgname-$pkgver.tar.xz")
md5sums=('968ac4d42a1a71754313527be2ab5df3')

prepare() {
	cd $pkgname-$pkgver
	sed -i 's,BUILD_GPERF := ,BUILD_GPERF := no #,' Make.Rules
	sed -i "/SBINDIR/s#sbin#bin#" Make.Rules
	sed -i "s/CFLAGS :=/CFLAGS += \$(CPPFLAGS) /" Make.Rules
}

build() {
	cd $pkgname-$pkgver
	make
}

package() {
	cd $pkgname-$pkgver
	make RAISE_SETFCAP=no lib=lib prefix=/usr DESTDIR=$pkgdir install

	chmod 755 $pkgdir/usr/lib/libcap.so

	mkdir -p $pkgdir/etc/security
	install -m644 pam_cap/capability.conf $pkgdir/etc/security
}
