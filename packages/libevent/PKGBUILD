# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=libevent
pkgver=2.1.8
pkgrel=1
pkgdesc="An event notification library"
arch=('x86_64' 'i586' 'aarch64' 'arm' 'mips' 'mipsel' 'powerpc64le' 'powerpc64' 'powerpc' 'sparc64' 'sparc' 's390x' 'microblaze' 'or1k' 'hppa' 'riscv64' 'riscv32')
url="http://libevent.org/"
license=('BSD')
depends=('libressl')
optdepends=('python2: to use event_rpcgen.py')
source=("https://github.com/libevent/libevent/releases/download/release-$pkgver-stable/$pkgname-$pkgver-stable.tar.gz"
	'libressl.patch')
md5sums=('f3eeaed018542963b7d2416ef1135ecc'
         'e54149ec1223bd38a153ea787352ad6f')

prepare() {
	cd $pkgname-$pkgver-stable
	patch -Np1 -i $srcdir/libressl.patch
}

build() {
	cd $pkgname-$pkgver-stable
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--disable-libevent-regress \
		--build=$CHOST
	make
}

package() {
	cd $pkgname-$pkgver-stable
	make DESTDIR=$pkgdir install
}
