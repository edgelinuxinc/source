# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=linux-headers
pkgver=4.19.2
pkgrel=1
pkgdesc="Header files and scripts for building modules for Linux kernel"
arch=('x86_64' 'i586' 'aarch64' 'arm' 'mips' 'mipsel' 'powerpc64le' 'powerpc64' 'powerpc' 'sparc64' 'sparc' 's390x' 'microblaze' 'or1k' 'hppa' 'riscv64' 'riscv32')
url="https://www.kernel.org/"
license=('GPL')
options=('!strip')
source=("https://cdn.kernel.org/pub/linux/kernel/v4.x/linux-$pkgver.tar.xz")
md5sums=('fe3487940018bacda66ac57f60ee36b9')

prepare() {
	cd linux-$pkgver
	make mrproper
}

package() {
	cd linux-$pkgver
	case $CARCH in
		x86_64)
			export XKARCH="x86_64"
			;;
		i586)
			export XKARCH="i386"
			;;
		aarch64)
			export XKARCH="arm64"
			;;
		mipsel|mips)
			export XKARCH="mips"
			;;
		powerpc64le|powerpc64|powerpc)
			export XKARCH="powerpc"
			;;
		sparc64|sparc)
			export XKARCH="sparc"
			;;
		s390x)
			export XKARCH="s390"
			;;
		microblaze)
			export XKARCH="microblaze"
			;;
		hppa)
			export XKARCH="parisc"
			;;
		riscv32|riscv64)
			export XKARCH="riscv"
			;;
		*)
			echo "Architecture is not set or is not supported"
			exit 1
	esac

	make ARCH=$XKARCH INSTALL_HDR_PATH=$pkgdir/usr headers_install
	find $pkgdir/usr/include \( -name .install -o -name ..install.cmd \) -delete
}
