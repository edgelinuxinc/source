# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=linux
pkgver=4.18.13
reiserfs4patchver=4.18.0
pkgrel=21
pkgdesc="The Linux kernel"
arch=('x86_64' 'i586' 'aarch64' 'arm')
url="https://www.kernel.org/"
license=('GPL')
options=('!strip')
makedepends=('kmod' 'bc' 'libelf' 'xz-utils' 'libressl')
source=("https://cdn.kernel.org/pub/linux/kernel/v4.x/$pkgname-$pkgver.tar.xz"
	"http://downloads.sourceforge.net/project/reiser4/reiser4-for-linux-4.x/reiser4-for-$reiserfs4patchver.patch.gz"
	'x86_64-config'
	'i586-config'
	'arm64-config'
	'arm-config'
	'60-linux.hook'
	'90-linux.hook')
md5sums=('fd508f503ffe20504fca9dccf41c4d09'
         '4aa8cdbac33df4bee88fadefa9cdb8c4'
         '4b715ed9b7b5be8f8f39b7f5a855407b'
         '1628443ef20299286b3cea62cb7d1890'
         '54917d77879b88ad5457f9e7f8ddb40d'
         '2e20ea98c8a8a4ccb5b070d937e9006d'
         '8e58bafc66f67c38f306d0f854f92a8b'
         'f99d0be58396fd6564c894f4511c37b2')

prepare() {
	cd $pkgname-$pkgver
	gzip -dc $srcdir/reiser4-for-$reiserfs4patchver.patch.gz | patch -p1

	msg "Preparing kernel..."
	make mrproper

	case $CARCH in
		x86_64)
			export XKARCH="x86_64"
			cp $srcdir/x86_64-config .config
			;;
		i586)
			export XKARCH="i386"
			cp $srcdir/i586-config .config
			;;
		aarch64)
			export XKARCH="arm64"
			cp $srcdir/arm64-config .config
			;;
		arm)
			export XKARCH="arm"
			cp $srcdir/arm-config .config
			;;
		*)
			echo "Architecture is not set or is not supported"
			exit 1
	esac

	make ARCH=$XKARCH olddefconfig
}

build() {
	msg "Building kernel..."

	cd $pkgname-$pkgver
	case $CARCH in
		x86_64)
			export XKARCH="x86_64"
			;;
		i586)
			export XKARCH="i386"
			;;
		aarch64)
			export XKARCH="arm64"
			;;
		arm)
			export XKARCH="arm"
			;;
		*)
			echo "Architecture is not set or is not supported"
			exit 1
	esac

	make ARCH=$XKARCH
}

package() {
	msg "Installing kernel contents to the $pkgdir directory..."

	cd $pkgname-$pkgver
	case $CARCH in
		x86_64)
			export XKARCH="x86_64"
			export IMG="arch/x86/boot/bzImage"
			;;
		i586)
			export XKARCH="i386"
			export IMG="arch/x86/boot/bzImage"
			;;
		aarch64)
			export XKARCH="arm64"
			export IMG="arch/arm64/boot/Image"
			;;
		arm)
			export XKARCH="arm"
			export IMG="arch/arm/boot/zImage"
			;;
		*)
			echo "Architecture is not set or is not supported"
			exit 1
	esac

	mkdir -p $pkgdir/usr/lib/modules/
	make ARCH=$XKARCH INSTALL_MOD_PATH=$pkgdir/usr modules_install

	mkdir -p $pkgdir/boot
	cp $IMG $pkgdir/boot/vmlinuz-linux

	case $CARCH in
		aarch64|arm)
			make ARCH=$XKARCH INSTALL_DTBS_PATH=$PKG/boot/dtbs dtbs_install
			;;
	esac

	install -dm755 $pkgdir/usr/share/libalpm/hooks/
	install -m644 $srcdir/*linux.hook $pkgdir/usr/share/libalpm/hooks/

	local subst="
		s|%KVER%|$pkgver|g
	"

	sed -i "$subst" $pkgdir/usr/share/libalpm/hooks/60-linux.hook
	sed -i "$subst" $pkgdir/usr/share/libalpm/hooks/90-linux.hook
}
