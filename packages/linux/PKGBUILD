# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=linux
pkgver=4.18.11
pkgrel=19
pkgdesc="The Linux kernel"
arch=('x86_64')
url="https://www.kernel.org/"
license=('GPL')
options=('!strip')
makedepends=('kmod' 'bc' 'libelf' 'xz-utils' 'libressl')
source=("https://cdn.kernel.org/pub/linux/kernel/v4.x/$pkgname-$pkgver.tar.xz"
	'x86_64-config'
	'arm64-config'
	'60-linux.hook'
	'90-linux.hook')
md5sums=('b193661a2c8afd41ba08fdbc55fe6a8c'
         'df364edfa4b7d7b7b14dd60ad2f2f2b0'
         '1b1fd53bc8c0595b2cc14f70c5d1857e'
         '8e58bafc66f67c38f306d0f854f92a8b'
         'f99d0be58396fd6564c894f4511c37b2')

prepare() {
	msg "Preparing kernel..."
	cd $pkgname-$pkgver
	make mrproper

	case $CARCH in
		x86_64)
			export XKARCH="x86_64"
			cp $srcdir/x86_64-config .config
			;;
		aarch64)
			export XKARCH="arm64"
			cp $srcdir/arm64-config .config
			;;
		*)
			echo "Architecture is not set or is not supported"
			exit 1
	esac
}

build() {
	msg "Building kernel..."

	cd $pkgname-$pkgver
	case $CARCH in
		x86_64)
			export XKARCH="x86_64"
			;;
		aarch64)
			export XKARCH="arm64"
			;;
		*)
			echo "Architecture is not set or is not supported"
			exit 1
	esac

	yes '' | make ARCH=$XKARCH
}

package() {
	msg "Installing kernel contents to the $pkgdir directory..."

	cd $pkgname-$pkgver
	case $CARCH in
		x86_64)
			export XKARCH="x86_64"
			export IMG="arch/x86/boot/bzImage"
			;;
		aarch64)
			export XKARCH="arm64"
			export IMG="arch/arm64/boot/Image"
			;;
		*)
			echo "Architecture is not set or is not supported"
			exit 1
	esac

	mkdir -p $pkgdir/usr/lib/modules/
	make ARCH=$XKARCH INSTALL_MOD_PATH=$pkgdir/usr modules_install

	mkdir -p $pkgdir/boot
	cp $IMG $pkgdir/boot/vmlinuz-linux

	case $CARCH in
		aarch64)
			make ARCH=$XKARCH INSTALL_DTBS_PATH=$PKG/boot/dtbs dtbs_install
			;;
	esac

	install -dm755 $pkgdir/usr/share/libalpm/hooks/
	install -m644 $srcdir/*linux.hook $pkgdir/usr/share/libalpm/hooks/

	local subst="
		s|%KVER%|$pkgver|g
	"

	sed -i "$subst" $pkgdir/usr/share/libalpm/hooks/60-linux.hook
	sed -i "$subst" $pkgdir/usr/share/libalpm/hooks/90-linux.hook
}
