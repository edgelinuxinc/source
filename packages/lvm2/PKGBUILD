# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=lvm2
pkgver=2.02.178
pkgrel=1
pkgdesc="Logical Volume Manager 2 utilities"
arch=('x86_64' 'aarch64')
url="https://sourceware.org/lvm2/"
license=('GPL' 'LGPL')
depends=('glibc' 'util-linux-libs' 'readline' 'libaio' 'systemd-libs')
backup=('etc/lvm/lvm.conf'
	'etc/lvm/lvmlocal.conf')
source=("https://sourceware.org/ftp/lvm2/releases/LVM2.$pkgver.tgz")
md5sums=('61f7d4c20b529bc5f65130372ea340cf')

prepare() {
	cd LVM2.$pkgver
	sed -i -e '/^\[Install\]$/,$d' \
		scripts/dm_event_systemd_red_hat.socket.in \
		scripts/lvm2_lvmetad_systemd_red_hat.socket.in \
		scripts/lvm2_lvmpolld_systemd_red_hat.socket.in \
		scripts/lvm2_monitoring_systemd_red_hat.service.in
}

build() {
	cd LVM2.$pkgver
	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--with-default-dm-run-dir=/run \
		--with-default-locking-dir=/run/lock/lvm \
		--with-default-pid-dir=/run \
		--with-default-run-dir=/run/lvm \
		--with-systemdsystemunitdir=/usr/lib/systemd/system \
		--with-thin=internal \
		--with-udev-prefix=/usr \
		--enable-applib \
		--enable-cmdlib \
		--enable-dmeventd \
		--enable-lvmetad \
		--enable-pkgconfig \
		--enable-udev_sync \
		--build=$CHOST
	make
}

package() {
	cd LVM2.$pkgver
	make DESTDIR=$pkgdir install
}
