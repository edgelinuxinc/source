# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=lzo
pkgver=2.10
pkgrel=1
pkgdesc="Portable lossless data compression library"
arch=('x86_64' 'aarch64')
url="http://www.oberhumer.com/opensource/lzo"
license=('GPL')
depends=('glibc')
source=("http://www.oberhumer.com/opensource/$pkgname/download/$pkgname-$pkgver.tar.gz")
md5sums=('39d3f3f9c55c87b1e5d6888e1420f4b5')

build() {
	cd $pkgname-$pkgver
	./configure \
		--prefix=/usr \
		--enable-shared \
		--build=$CHOST
	make

	cc $CFLAGS -fpic -Iinclude/lzo -o minilzo/minilzo.o -c minilzo/minilzo.c
	cc $LDFLAGS -shared -o libminilzo.so.0 -Wl,-soname,libminilzo.so.0 minilzo/minilzo.o
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR=$pkgdir install

	install -m 755 libminilzo.so.0 $pkgdir/usr/lib
	install -p -m 644 minilzo/minilzo.h $pkgdir/usr/include/lzo

	cd $pkgdir/usr/lib
	ln -s libminilzo.so.0 libminilzo.so
}
