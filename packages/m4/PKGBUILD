# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=m4
pkgver=1.4.18
pkgrel=1
pkgdesc="The GNU macro processor"
arch=('x86_64' 'i586' 'aarch64' 'arm' 'mips64' 'mips' 'powerpc64le' 'powerpc64' 'powerpc' 'sparc64' 's390x' 'microblaze' 'riscv64' 'riscv32')
url="http://www.gnu.org/software/m4"
license=('GPL')
depends=('glibc')
source=("http://ftpmirror.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz")
md5sums=('730bb15d96fffe47e148d1e09235af82')

prepare() {
	cd $pkgname-$pkgver
	sed -i 's/IO_ftrylockfile/IO_EOF_SEEN/' lib/*.c
	echo "#define _IO_IN_BACKUP 0x100" >> lib/stdio-impl.h
}

build() {
	cd $pkgname-$pkgver
	./configure \
		--prefix=/usr \
		--build=$CHOST
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR=$pkgdir install
}
