# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=mdadm
pkgver=4.1
pkgrel=1
pkgdesc="A tool for managing/monitoring Linux md device arrays, also known as Software RAID"
arch=('x86_64' 'i586' 'aarch64' 'arm' 'mips64' 'mips' 'powerpc64le' 'powerpc64' 'powerpc' 'sparc64' 'sparc' 's390x' 'microblaze' 'riscv64' 'riscv32')
url="http://neil.brown.name/blog/mdadm"
license=('GPL')
depends=('glibc')
backup=('etc/mdadm.conf')
source=("https://www.kernel.org/pub/linux/utils/raid/$pkgname/$pkgname-$pkgver.tar.xz"
	'mdadm.conf')
md5sums=('51bf3651bd73a06c413a2f964f299598'
         '5a37c112aa07dccdde62f9fa5b888607')

prepare() {
	cd $pkgname-$pkgver
	sed 's@-Werror@@' -i Makefile
}

build() {
	cd $pkgname-$pkgver
	make CXFLAGS="$CFLAGS" BINDIR=/usr/bin UDEVDIR=/usr/lib/udev
}

package() {
	cd $pkgname-$pkgver
	make INSTALL=/usr/bin/install BINDIR=/usr/bin UDEVDIR=/usr/lib/udev DESTDIR=$pkgdir install
	make SYSTEMD_DIR=$pkgdir/usr/lib/systemd/system install-systemd

	install -D -m644 $srcdir/mdadm.conf $pkgdir/etc/mdadm.conf
}
