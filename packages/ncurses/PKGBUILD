# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=ncurses
pkgver=6.1
pkgrel=1
pkgdesc="System V Release 4.0 curses emulation library"
arch=('x86_64' 'i586' 'aarch64' 'arm' 'mips64' 'mips' 'powerpc64le' 'powerpc64' 'powerpc' 'sparc64' 's390x' 'microblaze' 'riscv64' 'riscv32')
url="http://invisible-island.net/ncurses/ncurses.html"
license=('MIT')
depends=('glibc' 'gcc-libs')
source=("http://ftpmirror.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('98c889aaf8d23910d2b92d65be2e737a')

build() {
	cd $pkgname-$pkgver
	./configure \
		--prefix=/usr \
		--mandir=/usr/share/man \
		--with-pkg-config-libdir=/usr/lib/pkgconfig \
		--with-cxx-binding \
		--with-cxx-shared \
		--with-normal \
		--with-shared \
		--without-ada \
		--without-debug \
		--without-tests \
		--enable-pc-files \
		--enable-widec \
		--build=$CHOST
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR=$pkgdir install

	for lib in ncurses ncurses++ form panel menu; do
		echo "INPUT(-l${lib}w)" > $pkgdir/usr/lib/lib${lib}.so
		ln -sf ${lib}w.pc $pkgdir/usr/lib/pkgconfig/${lib}.pc
	done

	for lib in tic tinfo; do
		echo "INPUT(libncursesw.so.${pkgver:0:1})" > $pkgdir/usr/lib/lib${lib}.so
		ln -sf libncursesw.so.${pkgver:0:1} $pkgdir/usr/lib/lib${lib}.so.${pkgver:0:1}
	done

	echo 'INPUT(-lncursesw)' > $pkgdir/usr/lib/libcursesw.so
	ln -sf libncurses.so $pkgdir/usr/lib/libcurses.so
}
