# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=net-tools
pkgver=aebd88ef8d6e15f673b62a649a50d07ed727c888
pkgrel=2
pkgdesc="Configuration tools for Linux networking"
arch=('x86_64' 'i586' 'aarch64')
url="http://net-tools.sourceforge.net/"
license=('GPL')
depends=('glibc')
options=('!makeflags')
source=("https://github.com/protonesso/net-tools/archive/$pkgver.tar.gz")
md5sums=('8862c662e9456566e50cab226d570810')

prepare() {
	cd $pkgname-$pkgver
	sed -i "s#/sbin#/bin#" Makefile
	sed -i "s#/usr##" man/Makefile
}

build() {
	cd $pkgname-$pkgver
	yes "" | make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR=$pkgdir/usr update

	rm $pkgdir/usr/bin/{nis,yp}domainname
}
