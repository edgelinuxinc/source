# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=('ocaml' 'ocaml-compiler-libs')
pkgbase=ocaml
pkgver=4.07.0
pkgrel=2
pkgdesc="A functional language with OO extensions"
arch=('x86_64' 'i586' 'aarch64' 'arm' 'riscv64' 'riscv32')
url="http://caml.inria.fr/"
license=('GPL' 'custom: QPL')
depends=('glibc' 'gdbm')
makedepends=('zlib' 'ncurses' 'gdbm')
options=('!makeflags' '!emptydirs' 'staticlibs')
source=("http://caml.inria.fr/distrib/${pkgbase}-${pkgver%.*}/${pkgbase}-${pkgver}.tar.xz")
md5sums=('24f3cd1f9da825f8b3baabee89079de4')

build() {
	cd $pkgbase-$pkgver
	./configure \
		-prefix /usr
	make -j1 world.opt
}

package_ocaml() {
	cd $pkgbase-$pkgver
	make PREFIX=$pkgdir/usr install

	rm -rf $pkgdir/usr/lib/ocaml/compiler-libs
}

package_ocaml-compiler-libs() {
	pkgdesc="Several modules used internally by the OCaml compiler"
	license=('custom: QPL-1.0')
	depends=('ocaml')

	cd $pkgbase-$pkgver

	BYTESTART=driver/main.cmo
	TOPLEVELSTART=toplevel/topstart.cmo

	mkdir -p $pkgdir/usr/lib/ocaml/compiler-libs

	cp utils/*.cmi utils/*.cmt utils/*.cmti utils/*.mli \
		parsing/*.cmi parsing/*.cmt parsing/*.cmti parsing/*.mli \
		typing/*.cmi typing/*.cmt typing/*.cmti typing/*.mli \
		bytecomp/*.cmi bytecomp/*.cmt bytecomp/*.cmti bytecomp/*.mli \
		driver/*.cmi driver/*.cmt driver/*.cmti driver/*.mli \
		toplevel/*.cmi toplevel/*.cmt toplevel/*.cmti toplevel/*.mli \
		$pkgdir/usr/lib/ocaml/compiler-libs

	cp compilerlibs/* ${BYTESTART} ${TOPLEVELSTART} \
		$pkgdir/usr/lib/ocaml/compiler-libs
}
