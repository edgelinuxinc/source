# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=pam
pkgver=1.3.1
pkgrel=6
pkgdesc="PAM (Pluggable Authentication Modules) library"
arch=('x86_64' 'aarch64')
url="http://linux-pam.org"
license=('GPL')
depends=('glibc' 'cracklib')
makedepends=('bison' 'flex')
options=('!emptydirs')
backup=('etc/security/{access.conf,group.conf,limits.conf,namespace.conf,namespace.init,pam_env.conf,time.conf}'
	'etc/default/passwd'
	'etc/environment'
	'etc/pam.d/{other,system-account,system-auth,system-password,system-session}')
source=(https://github.com/linux-pam/linux-pam/releases/download/v$pkgver/Linux-PAM-$pkgver.tar.xz
	other
	system-account
	system-auth
	system-password
	system-session)
md5sums=('558ff53b0fc0563ca97f79e911822165'
         '38e5773861ac415f9e839d9602855c3e'
         '0fa7b59aa9d33b4d3e8b97cf0f67df0e'
         '9da98a8a965bc25d212d5de700bd3dc3'
         '2e1722b942f643231a9a5f7921b39623'
         'ca07b6b9ce1e2d655ca0e323d38a9e44')

build() {
	cd Linux-PAM-$pkgver
	./configure \
		--prefix=/usr \
		--libdir=/usr/lib \
		--sbindir=/usr/bin \
		--sysconfdir=/etc \
		--disable-db \
		--disable-regenerate-docu \
		--build=$CHOST
	make
}

package() {
	cd Linux-PAM-$pkgver
	make DESTDIR=$pkgdir install

	chmod +s $pkgdir/usr/bin/unix_chkpwd

	mkdir -p $pkgdir/etc/pam.d
	for f in other system-account system-auth system-password system-session; do
		install -m644 $srcdir/$f $pkgdir/etc/pam.d/
	done
}
