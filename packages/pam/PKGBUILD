# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=pam
pkgver=1.3.1
pkgrel=9
pkgdesc="PAM (Pluggable Authentication Modules) library"
arch=('x86_64' 'i586' 'aarch64' 'arm' 'riscv32' 'riscv64')
url="http://linux-pam.org"
license=('GPL')
depends=('glibc' 'cracklib')
makedepends=('bison' 'flex')
options=('!emptydirs')
backup=('etc/security/{access.conf,group.conf,limits.conf,namespace.conf,namespace.init,pam_env.conf,time.conf}'
	'etc/environment')
source=(https://github.com/linux-pam/linux-pam/releases/download/v$pkgver/Linux-PAM-$pkgver.tar.xz)
md5sums=('558ff53b0fc0563ca97f79e911822165')

build() {
	cd Linux-PAM-$pkgver
	./configure \
		--prefix=/usr \
		--libdir=/usr/lib \
		--sbindir=/usr/bin \
		--sysconfdir=/etc \
		--build=$CHOST
	make
}

package() {
	cd Linux-PAM-$pkgver
	make DESTDIR=$pkgdir install

	ln -sf pam_unix.so $pkgdir/usr/lib/security/pam_unix_auth.so
	ln -sf pam_unix.so $pkgdir/usr/lib/security/pam_unix_acct.so
	ln -sf pam_unix.so $pkgdir/usr/lib/security/pam_unix_passwd.so
	ln -sf pam_unix.so $pkgdir/usr/lib/security/pam_unix_session.so

	chmod 4755 $pkgdir/usr/bin/unix_chkpwd
}
