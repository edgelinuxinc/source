# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=pam
pkgver=1.3.1
pkgrel=9
pkgdesc="PAM (Pluggable Authentication Modules) library"
arch=('x86_64' 'i586' 'aarch64' 'arm' 'mips64' 'mips' 'powerpc64le' 'powerpc64' 'powerpc' 'sparc64' 's390x' 'microblaze' 'riscv64' 'riscv32')
url="http://linux-pam.org"
license=('GPL')
depends=('glibc' 'cracklib')
makedepends=('bison' 'flex')
options=('!emptydirs')
backup=(etc/security/{access.conf,group.conf,limits.conf,namespace.conf,namespace.init,pam_env.conf,time.conf}
	etc/environment
	etc/pam.d/{common-account,common-auth,common-password,common-session,common-session-noninteractive,login,other,sshd,su,sudo,su-l,system-auth})
source=("https://github.com/linux-pam/linux-pam/releases/download/v$pkgver/Linux-PAM-$pkgver.tar.xz"
	'common-account'
	'common-auth'
	'common-password'
	'common-session'
	'common-session-noninteractive'
	'login'
	'other'
	'sshd'
	'su'
	'su-l'
	'sudo'
	'system-auth')
md5sums=('558ff53b0fc0563ca97f79e911822165'
         'f4cc7c31e7a4e8a6b399b4d4dc04ccb7'
         'be2518f2a1903c5c80e598ef6307399a'
         '7e2103cd72a784640103601dc4875da4'
         '9d93ccc13227683f23558775d25e8a1b'
         '6f501127d2db7fe9d5615123a49fb595'
         'de5b5124e65c3be5f6d30e57fe743d2d'
         'dcaab4390b80b0d53ad59aa83e3b4aa1'
         'b77df331c028555596748dd5abda3e66'
         '15e94f9472da2681a475998047fea6c5'
         '15e94f9472da2681a475998047fea6c5'
         '248549b2be570ee42ab87e67e3fa66d5'
         'f328a40249fd512e635d2080cae49e3c')

build() {
	cd Linux-PAM-$pkgver
	./configure \
		--prefix=/usr \
		--libdir=/usr/lib \
		--sbindir=/usr/bin \
		--sysconfdir=/etc \
		--build=$CHOST
	make
}

package() {
	cd Linux-PAM-$pkgver
	make DESTDIR=$pkgdir install

	ln -sf pam_unix.so $pkgdir/usr/lib/security/pam_unix_auth.so
	ln -sf pam_unix.so $pkgdir/usr/lib/security/pam_unix_acct.so
	ln -sf pam_unix.so $pkgdir/usr/lib/security/pam_unix_passwd.so
	ln -sf pam_unix.so $pkgdir/usr/lib/security/pam_unix_session.so

	chmod 4755 $pkgdir/usr/bin/unix_chkpwd

	install -d $pkgdir/etc/pam.d

	for pam in common-account common-auth common-password common-session common-session-noninteractive login other sshd su su-l sudo system-auth; do
		install -m 0644 $srcdir/$pam $pkgdir/etc/pam.d/
	done
}
