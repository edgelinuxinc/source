# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=polkit
pkgver=0.114
pkgrel=5
pkgdesc="Application development toolkit for controlling system-wide privileges"
arch=('x86_64' 'i586' 'aarch64' 'arm' 'mips' 'mipsel' 'powerpc64le' 'powerpc64' 'powerpc' 'sparc64' 'sparc' 's390x' 'microblaze' 'or1k' 'hppa' 'riscv64' 'riscv32')
url="https://www.freedesktop.org/wiki/Software/polkit/"
license=('LGPL')
depends=('glibc' 'pam' 'expat' 'systemd' 'glib' 'js52')
makedepends=('intltool' 'gobject-introspection' 'autoconf-archive')
install=$pkgname.install
source=("https://www.freedesktop.org/software/$pkgname/releases/$pkgname-$pkgver.tar.gz"
	'polkit.pam')
md5sums=('93ff41874e7df8c62ed9e41893817f04'
         '82de0804151532174dc3b55ef169f87b')

prepare() {
	cd $pkgname-$pkgver
	sed -i "s:/sys/fs/cgroup/systemd/:/sys:g" configure
	sed -e '/JS_ReportWarningUTF8/s/,/, "%s",/' \
		-i  src/polkitbackend/polkitbackendjsauthority.cpp
}

build() {
	cd $pkgname-$pkgver
	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--with-os-type=redhat \
		--enable-libsystemd-login=yes \
		--build=$CHOST
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR=$pkgdir install

	install -D -m0644 $srcdir/polkit.pam $pkgdir/etc/pam.d/polkit-1

	install -d -o root -g 102 -m 750 $pkgdir/{etc,usr/share}/polkit-1/rules.d
}
