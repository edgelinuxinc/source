# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=procps-ng
pkgver=3.3.15
pkgrel=3
pkgdesc="Utilities for monitoring your system and its processes"
arch=('x86_64' 'i586' 'aarch64' 'armv7h' 'armv6h' 'armv5tel' 'powerpc64le' 'powerpc64' 'powerpc' 'sparc64' 's390x' 'riscv64')
url="https://gitlab.com/procps-ng/procps"
license=('GPL' 'LGPL')
depends=('glibc' 'ncurses' 'systemd-libs')
source=("https://downloads.sourceforge.net/project/$pkgname/Production/$pkgname-$pkgver.tar.xz")
md5sums=('2b0717a7cb474b3d6dfdeedfbad2eccc')

prepare() {
	cd $pkgname-$pkgver
	sed 's:<ncursesw/:<:g' -i watch.c
}

build() {
	cd $pkgname-$pkgver
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--libdir=/usr/lib \
		--bindir=/usr/bin \
		--sbindir=/usr/bin \
		--exec-prefix=/ \
		--with-systemd \
		--enable-watch8bit \
		--disable-kill \
		--build=$CHOST
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR=$pkgdir install

	rm -rf $pkgdir/usr/bin/top
}
