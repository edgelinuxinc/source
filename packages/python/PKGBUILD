# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=python
pkgver=3.7.1
pkgrel=6
pkgdesc="Next generation of the python high-level scripting language"
arch=('x86_64' 'i586' 'aarch64' 'arm' 'mips' 'mipsel' 'powerpc64le' 'powerpc64' 'powerpc' 'sparc64' 'sparc' 's390x' 'microblaze' 'or1k' 'hppa' 'riscv64' 'riscv32')
url="http://www.python.org/"
license=('custom')
depends=('glibc' 'zlib' 'bzip2' 'gdbm' 'readline' 'libressl' 'expat' 'libffi' 'sqlite')
source=(https://www.python.org/ftp/python/${pkgver%rc*}/Python-${pkgver}.tar.xz)
md5sums=('0a57e9022c07fad3dadb2eef58568edb')

prepare() {
	cd Python-${pkgver}

	rm -rf Modules/expat Modules/zlib Modules/_ctypes/libffi*
}

build() {
	cd Python-${pkgver}
	CXX="/usr/bin/g++" \
	./configure \
		--prefix=/usr \
		--with-computed-gotos \
		--with-dbmliborder=gdbm:ndbm \
		--with-ensurepip=yes \
		--with-lto=8 \
		--with-system-expat \
		--with-system-ffi \
		--enable-ipv6 \
		--enable-loadable-sqlite-extensions \
		--enable-shared \
		--build=$CHOST
	make
}

package() {
	cd Python-${pkgver}
	sed -i 's/^all:.*$/all: build_all/' Makefile

	make DESTDIR=$pkgdir install

	chmod 755 $pkgdir/usr/lib/libpython3.{7m.,}so
}
