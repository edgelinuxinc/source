# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=python2
pkgver=2.7.15
pybasever=2.7
pkgrel=4
pkgdesc="A high-level scripting language"
arch=('x86_64' 'i586' 'aarch64' 'arm' 'mips' 'mipsel' 'powerpc64le' 'powerpc64' 'powerpc' 'sparc64' 'sparc' 's390x' 'microblaze' 'or1k' 'hppa' 'riscv64' 'riscv32')
url="http://www.python.org/"
license=('custom')
depends=('glibc' 'zlib' 'bzip2' 'gdbm' 'readline' 'libressl' 'expat' 'libffi' 'sqlite')
source=(https://www.python.org/ftp/python/${pkgver%rc*}/Python-${pkgver}.tar.xz)
md5sums=('a80ae3cc478460b922242f43a1b4094d')

prepare() {
	cd Python-${pkgver}
	rm -rf Modules/expat Modules/zlib Modules/_ctypes/libffi*

	sed -i "/SQLITE_OMIT_LOAD_EXTENSION/d" setup.py
}

build() {
	cd Python-${pkgver}
	./configure \
		--prefix=/usr \
		--with-computed-gotos \
		--with-dbmliborder=gdbm:ndbm \
		--with-ensurepip=yes \
		--with-lto \
		--with-system-expat \
		--with-system-ffi \
		--with-threads \
		--enable-ipv6 \
		--enable-shared \
		--enable-unicode=ucs4 \
		--build=$CHOST
	make
}

package() {
	cd Python-${pkgver}
	sed -i 's/^all:.*$/all: build_all/' Makefile

	make DESTDIR=$pkgdir install

	chmod 755 $pkgdir/usr/lib/libpython2.7.so.1.0

	mv $pkgdir/usr/bin/idle{,2}
	mv $pkgdir/usr/bin/pydoc{,2}
	mv $pkgdir/usr/bin/2to3{,-2.7}
}
