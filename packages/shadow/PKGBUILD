# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=shadow
pkgver=4.6
pkgrel=1
pkgdesc="Password and account management tool suite with support for shadow files and PAM"
arch=('x86_64' 'i586' 'aarch64' 'arm' 'mips64' 'mips' 'powerpc64le' 'powerpc64' 'powerpc' 'sparc64' 'sparc' 's390x' 'microblaze' 'riscv64' 'riscv32')
url="https://github.com/shadow-maint/shadow"
license=('BSD')
depends=('glibc' 'pam' 'acl')
options=('strip' 'debug')
backup=(etc/pam.d/{chage,chfn,chgpasswd,chpasswd,chsh,groupadd,groupdel,groupmems,groupmod,newusers,passwd,useradd,userdel,usermod}
	etc/default/useradd
	etc/login.defs)
source=("https://github.com/shadow-maint/shadow/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
	'0010-Make-glibc-give-up-memory-we-have-already-released.patch'
	'chfn'
	'chpasswd'
	'chsh'
	'newusers'
	'passwd'
	'service'
	'timer')
md5sums=('b491fecbf1232632c32ff8f1437fd60e'
         '4e1b71cfa7689cd41d36df7de0bd7666'
         'b35dec039ede7171227eae9b2385e833'
         '49f61a503d1bfaac2f39cf3bc73b02ad'
         'f07a9370ec5815b6684cc4607d5ee55c'
         '4836abb6f7820eac68cc854b4dfae303'
         '4836abb6f7820eac68cc854b4dfae303'
         'f08ebd358139e9f74e1b90f7231dc541'
         '5bd14862ace022d6682b179c18fe30de')

prepare() {
	cd $pkgname-$pkgver
	patch -p1 -i $srcdir/0010-Make-glibc-give-up-memory-we-have-already-released.patch
	sed -i 's/groups$(EXEEXT) //' src/Makefile.in
	find man -name Makefile.in -exec sed -i 's/groups\.1 / /'   {} \;
	find man -name Makefile.in -exec sed -i 's/getspnam\.3 / /' {} \;
	find man -name Makefile.in -exec sed -i 's/passwd\.5 / /'   {} \;
	sed -i -e 's@#ENCRYPT_METHOD DES@ENCRYPT_METHOD SHA512@' -e 's@/var/spool/mail@/var/mail@' etc/login.defs
	sed -i 's/1000/100/' etc/useradd
}

build() {
	export CFLAGS="${CFLAGS/-O3/-Os}"
	export CXXFLAGS="${CXXFLAGS/-O3/-Os}"

	cd $pkgname-$pkgver
	./configure \
		--prefix=/usr \
		--bindir=/usr/bin \
		--sbindir=/usr/bin \
		--libdir=/usr/lib \
		--mandir=/usr/share/man \
		--sysconfdir=/etc \
		--with-group-name-max-length=32 \
		--with-libpam \
		--without-audit \
		--without-libcrack \
		--without-selinux \
		--disable-rpath \
		--build=$CHOST
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR=$pkgdir install

	rm -r $pkgdir/etc/pam.d/login $pkgdir/etc/pam.d/su

	mkdir -p pam.d
	install $srcdir/chfn pam.d/
	install $srcdir/chpasswd pam.d/
	install $srcdir/chsh pam.d/
	install $srcdir/newusers pam.d/
	install $srcdir/passwd pam.d/

	install -d $pkgdir/etc/pam.d/
	install -m 0644 pam.d/* $pkgdir/etc/pam.d/

	install -D -m644 $srcdir/timer $pkgdir/usr/lib/systemd/system/shadow.timer
	install -D -m644 $srcdir/service $pkgdir/usr/lib/systemd/system/shadow.service
	install -d -m755 $pkgdir/usr/lib/systemd/system/timers.target.wants
	ln -sf ../shadow.timer $pkgdir/usr/lib/systemd/system/timers.target.wants/shadow.timer

	mv $pkgdir/usr/sbin/* $pkgdir/usr/bin
	rmdir $pkgdir/usr/sbin

	ln -sf newgrp $pkgdir/usr/bin/sg

	rm -rf $pkgdir/usr/bin/su
	find $pkgdir -type f -name 'su.1' -exec rm {} \;
}
