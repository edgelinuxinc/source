# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=shadow
pkgver=4.6
pkgrel=3
pkgdesc="Password and account management tool suite"
arch=('x86_64' 'i586' 'aarch64' 'arm')
url="https://github.com/shadow-maint/shadow"
license=('BSD')
depends=('glibc' 'pam' 'acl')
backup=('etc/default/useradd'
	'etc/limits'
	'etc/login.defs'
	'etc/login.access'
	'etc/pam.d/{chage,login,other,passwd,su,system-account,system-auth,system-password,system-session}'
	'etc/pam.d/{chfn,chgpasswd,chpasswd,chsh,groupadd,groupdel,groupmems,groupmod,newusers,useradd,userdel,usermod}')
source=("https://github.com/shadow-maint/shadow/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
	'chage'
	'login'
	'other'
	'passwd'
	'su'
	'system-account'
	'system-auth'
	'system-password'
	'system-session')
md5sums=('b491fecbf1232632c32ff8f1437fd60e'
         '34f473198e205271c100293e18367e94'
         '350589010bcbb9cb59165ca6f2752c8b'
         '71561ee65e9cfbb212c7df66405789af'
         '9c784e04e81e43d7a6f9c9a202803803'
         'e450bf7603abfaf06fdb3a551a7b7c08'
         '54c6efd6559371c2db72c3a5deeffc4e'
         '963b6df743077b7407ad6bcc416edacc'
         'e61abeddf532895504686eb88850bfda'
         '3dd163ec12f1b66818af68b3e0f24a85')

prepare() {
	cd $pkgname-$pkgver
	sed -i 's/groups$(EXEEXT) //' src/Makefile.in &&

	find man -name Makefile.in -exec sed -i 's/groups\.1 / /'   {} \; &&
	find man -name Makefile.in -exec sed -i 's/getspnam\.3 / /' {} \; &&
	find man -name Makefile.in -exec sed -i 's/passwd\.5 / /'   {} \; &&

	sed -i -e 's@#ENCRYPT_METHOD DES@ENCRYPT_METHOD SHA512@' \
		-e 's@/var/spool/mail@/var/mail@' etc/login.defs &&

	sed -i 's/1000/100/' etc/useradd
}

build() {
	cd $pkgname-$pkgver
	./configure \
		--prefix=/usr \
		--libdir=/usr/lib \
		--bindir=/usr/bin \
		--sbindir=/usr/bin \
		--sysconfdir=/etc \
		--mandir=/usr/share/man \
		--with-libpam \
		--with-group-name-max-length=32 \
		--without-selinux \
		--build=$CHOST
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR=$pkgdir install

	sed -i 's/yes/no/' $pkgdir/etc/default/useradd

	for FUNCTION in FAIL_DELAY \
		FAILLOG_ENAB \
		LASTLOG_ENAB \
		MAIL_CHECK_ENAB \
		OBSCURE_CHECKS_ENAB \
		PORTTIME_CHECKS_ENAB \
		QUOTAS_ENAB \
		CONSOLE MOTD_FILE \
		FTMP_FILE NOLOGINS_FILE \
		ENV_HZ PASS_MIN_LEN \
		SU_WHEEL_ONLY \
		CRACKLIB_DICTPATH \
		PASS_CHANGE_TRIES \
		PASS_ALWAYS_WARN \
		CHFN_AUTH ENCRYPT_METHOD \
		ENVIRON_FILE
	do
		sed -i "s/^${FUNCTION}/# &/" $pkgdir/etc/login.defs
	done

	mkdir -p $pkgdir/etc/pam.d/
	for PROGRAM in chage login other passwd su system-account system-auth system-password system-session; do
		install -m644 $srcdir/${PROGRAM} $pkgdir/etc/pam.d/${PROGRAM}
	done

	for PROGRAM in chfn chgpasswd chpasswd chsh groupadd groupdel groupmems groupmod newusers useradd userdel usermod; do
		install -m644 $pkgdir/etc/pam.d/chage $pkgdir/etc/pam.d/${PROGRAM}
		sed -i "s/chage/$PROGRAM/" $pkgdir/etc/pam.d/${PROGRAM}
	done

	rm -rf $pkgdir/run/nologin

	[ -f $pkgdir/etc/login.access ] && mv $pkgdir/etc/login.access{,.NOUSE}
	[ -f $pkgdir/etc/limits ] && mv $pkgdir/etc/limits{,.NOUSE}

	rm $pkgdir/usr/sbin/logoutd

	mv $pkgdir/usr/sbin/* $pkgdir/usr/bin
	rmdir $pkgdir/usr/sbin
}
