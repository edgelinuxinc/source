# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=shadow
pkgver=4.6
pkgrel=6
pkgdesc="Password and account management tool"
arch=('x86_64' 'aarch64')
url="https://github.com/shadow-maint/shadow"
license=('BSD')
depends=('glibc' 'pam' 'acl')
backup=('etc/login.defs'
	'etc/pam.d/{login,passwd,su,chage}'
	'etc/pam.d/{chfn,chgpasswd,chpasswd,chsh,groupadd,groupdel,groupmems,groupmod,newusers,useradd,userdel,usermod}'
	'etc/default/useradd')
options=('strip' 'debug')
source=(https://github.com/shadow-maint/shadow/releases/download/$pkgver/$pkgname-$pkgver.tar.xz
	service
	timer
	login
	passwd
	su
	chage)
md5sums=('b491fecbf1232632c32ff8f1437fd60e'
         'f08ebd358139e9f74e1b90f7231dc541'
         '5bd14862ace022d6682b179c18fe30de'
         '46c764be2f3a01f81731c13d7ac3a322'
         '1217d8faab1339fededb871a08622252'
         'c1618f83b159e70746efa4d3c03e89f0'
         '5164aec719f267435573a548425e3689')

prepare() {
	cd $pkgname-$pkgver
	sed -i 's/groups$(EXEEXT) //' src/Makefile.in
	find man -name Makefile.in -exec sed -i 's/groups\.1 / /' {} \;
	sed -i -e 's@#ENCRYPT_METHOD DES@ENCRYPT_METHOD SHA512@' \
		-e 's@/var/spool/mail@/var/mail@' etc/login.defs
	sed -i 's@DICTPATH.*@DICTPATH\t/usr/lib/cracklib/pw_dict@' \
		etc/login.defs
}

build() {
	cd $pkgname-$pkgver
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--bindir=/usr/bin \
		--sbindir=/usr/bin \
		--libdir=/usr/lib \
		--mandir=/usr/share/man \
		--with-group-name-max-length=32 \
		--with-libpam \
		--without-selinux \
		--build=$CHOST
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR=$pkgdir install

	sed -i 's/yes/no/' $pkgdir/etc/default/useradd
	sed -i 's/GROUP.*/GROUP=100/' $pkgdir/etc/default/useradd
	sed -i 's/USERGROUPS_ENAB.*/USERGROUPS_ENAB no/' $pkgdir/etc/login.defs

	cp etc/{limits,login.access} $pkgdir/etc

	for FUNCTION in FAIL_DELAY \
		AILLOG_ENAB  \
		LASTLOG_ENAB \
		MAIL_CHECK_ENAB \
		OBSCURE_CHECKS_ENAB \
		PORTTIME_CHECKS_ENAB \
		QUOTAS_ENAB \
		CONSOLE MOTD_FILE \
		FTMP_FILE NOLOGINS_FILE \
		ENV_HZ PASS_MIN_LEN \
		SU_WHEEL_ONLY \
		CRACKLIB_DICTPATH \
		PASS_CHANGE_TRIES \
		PASS_ALWAYS_WARN \
		CHFN_AUTH ENCRYPT_METHOD \
		ENVIRON_FILE
	do
		sed -i "s/^${FUNCTION}/# &/" $pkgdir/etc/login.defs
	done

	sed -i "s/^PASS_MAX_DAYS.*/PASS_MAX_DAYS 90/" $pkgdir/etc/login.defs

	install -D -m644 $srcdir/timer $pkgdir/usr/lib/systemd/system/shadow.timer
	install -D -m644 $srcdir/service $pkgdir/usr/lib/systemd/system/shadow.service
	install -d -m755 $pkgdir/usr/lib/systemd/system/timers.target.wants
	ln -sf ../shadow.timer $pkgdir/usr/lib/systemd/system/timers.target.wants/shadow.timer

	mkdir -p $pkgdir/etc/pam.d
	for f in login passwd su chage; do
		install -m644 $srcdir/$f $pkgdir/etc/pam.d/
	done

	for PROGRAM in chfn chgpasswd chpasswd chsh groupadd groupdel groupmems groupmod newusers useradd userdel usermod; do
		install -m644 $pkgdir/etc/pam.d/chage $pkgdir/etc/pam.d/${PROGRAM}
		sed -i "s/chage/$PROGRAM/" $pkgdir/etc/pam.d/${PROGRAM}
	done

	mv $pkgdir/usr/sbin/* $pkgdir/usr/bin
	rmdir $pkgdir/usr/sbin
}
