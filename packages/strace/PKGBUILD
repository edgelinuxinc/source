# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=strace
pkgver=4.25
pkgrel=1
pkgdesc="A diagnostic, debugging and instructional userspace tracer"
arch=('x86_64' 'i586' 'aarch64' 'arm' 'mips64' 'mips' 'powerpc64le' 'powerpc64' 'powerpc' 'sparc64' 's390x' 'microblaze' 'riscv64' 'riscv32')
url="https://strace.io/"
license=('BSD')
depends=('glibc' 'perl')
source=("https://github.com/strace/strace/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz")
md5sums=('fa5ac841e9ce6af133061822898d49a9')

prepare() {
	cd $pkgname-$pkgver
	sed -i -e 's/include <linux\/socket.h>/include <sys\/socket.h>/g' configure
	sed -i -e 's/include <sgidefs.h>/include <asm\/sgidefs.h>/g' configure
}

build() {
	cd $pkgname-$pkgver
	./configure \
		--prefix=/usr \
		--enable-mpers=no \
		--build=$CHOST
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR=$pkgdir install
}
