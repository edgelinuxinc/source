# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=syslinux
pkgver=6.03
pkgrel=1
pkgdesc="Collection of boot loaders that boot from FAT, ext2/3/4 and btrfs filesystems, from CDs and via PXE"
arch=('x86_64' 'i586')
url="http://www.syslinux.org/"
license=('GPL')
depends=('util-linux' 'perl')
makedepends=('python2' 'nasm' 'asciidoc')
options=('!makeflags' '!buildflags')
backup=('boot/syslinux/syslinux.cfg')
source=("https://mirrors.edge.kernel.org/pub/linux/utils/boot/$pkgname/$pkgname-$pkgver.tar.xz"
	'syslinux.cfg'
	'install-syslinux'
	'btrfs-fix.patch::http://repo.or.cz/syslinux.git/patch/548386049cd41e887079cdb904d3954365eb28f3?hp=721a0af2f0ba111c31685c5f6c5481eb25346971'
	'gcc-fix-alignment.patch::http://repo.or.cz/syslinux.git/patch/e5f2b577ded109291c9632dacb6eaa621d8a59fe?hp=8dc6d758b564a1ccc44c3ae11f265d43628219ce'
	'dont-guess-alignment.patch::http://repo.or.cz/syslinux.git/patch/0cc9a99e560a2f52bcf052fd85b1efae35ee812f?hp=e5f2b577ded109291c9632dacb6eaa621d8a59fe'
	'kdb-230.patch::http://repo.or.cz/syslinux.git/patch/138e850fab106b5235178848b3e0d33e25f4d3a2'
	'correct_base_type.patch::http://repo.or.cz/syslinux.git/patch/83aad4f'
	'set_mode_base.patch::http://repo.or.cz/syslinux.git/patch/0a2dbb3'
	'fix_return_pointer.patch::http://repo.or.cz/syslinux.git/patch/8dc6d758b564a1ccc44c3ae11f265d43628219ce'
	'support-ext4-64bit.patch::http://repo.or.cz/syslinux.git/patch/af7e95c32cea40c1e443ae301e64b27f068b4915'
	'efi_no_kbd.patch::http://repo.or.cz/syslinux.git/patch/73bb3701a19a9eeb30b6f77ff7612ca2a9cd3f38'
	'fix-build-with-glibc-2.28.patch')
md5sums=('92a253df9211e9c20172796ecf388f13'
         'e5800d8a0a8b51fcafa0689b948cdecc'
         'af525fc2082af52df5be7d80fb013b2e'
         '9935ec4501268ac0c99121877ef6d1b8'
         '93b5b4a5deca4fc90993bc5459640b04'
         '503d5f485b44883655d263c403015bca'
         '75bca0fe3a64e075278774e60cf4d49c'
         '2bddc02664ca79145d23f72dd71f615f'
         '09b59cf6a864a615161e49b938faf8fd'
         '8a73cd4c527561fc88485c1dd0ad76be'
         '7ba972373a21047db5c003e18d1c7b2a'
         '59d6c2b2893b310cb9b9a87c95bb0902'
         '7fb9840e8eb38f49bdd576f9f7be9d3d')

export targets='bios efi32'
case "$CARCH" in
	x86_64) export targets+=' efi64' ;;
esac

prepare() {
	cd $pkgname-$pkgver
	patch -p1 < $srcdir/gcc-fix-alignment.patch
	patch -p1 < $srcdir/dont-guess-alignment.patch
	patch -p1 < $srcdir/btrfs-fix.patch
	patch -p1 < $srcdir/kdb-230.patch
	patch -p1 < $srcdir/correct_base_type.patch
	patch -p1 < $srcdir/set_mode_base.patch
	patch -p1 < $srcdir/fix_return_pointer.patch
	patch -p1 < $srcdir/support-ext4-64bit.patch
	patch -p1 < $srcdir/efi_no_kbd.patch
	patch -p1 < $srcdir/fix-build-with-glibc-2.28.patch
	sed 's|> /dev/null 2>&1||' -i efi/check-gnu-efi.sh
	truncate --size 0 mk/devel.mk
}

build() {
	cd $pkgname-$pkgver
	export LDFLAGS+=--no-dynamic-linker
	export EXTRA_CFLAGS=-fno-PIE
	make PYTHON=python2 $targets
}

package() {
	cd $pkgname-$pkgver
	make $targets install INSTALLROOT=$pkgdir SBINDIR=/usr/bin MANDIR=/usr/share/man AUXDIR=/usr/lib/syslinux

	rm -r $pkgdir/usr/lib/syslinux/{com32,dosutil,syslinux.com}

	install -d $pkgdir/usr/lib/syslinux/bios
	mv $pkgdir/usr/lib/syslinux/{*.bin,*.c32,*.0,memdisk} $pkgdir/usr/lib/syslinux/bios 

	install -D -m0644 $srcdir/syslinux.cfg $pkgdir/boot/syslinux/syslinux.cfg
	install -D -m0755 $srcdir/install-syslinux $pkgdir/usr/bin/install-syslinux
}
