# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=tzdata
pkgver=2018g
_tzcode=2018g
_tzdata=2018g
pkgrel=1
pkgdesc="Sources for time zone and daylight saving time data"
arch=('x86_64' 'i586' 'aarch64' 'arm' 'riscv64' 'riscv32')
url="http://www.iana.org/time-zones"
license=('GPL')
options=('!emptydirs')
source=(https://www.iana.org/time-zones/repository/releases/tzcode${_tzcode}.tar.gz
	https://www.iana.org/time-zones/repository/releases/${pkgname}${_tzdata}.tar.gz)
md5sums=('b48f0282b80bb7dbe16e35626f446ae9'
         'e71cb1f9d8d53c43904d79d7aeeedc1b')
timezones=('africa' 'antarctica' 'asia' 'australasia'
	'europe' 'northamerica' 'southamerica'
	'pacificnew' 'etcetera' 'backward'
	'systemv' 'factory')

prepare() {
	sed -i "s:sbin:bin:g" Makefile
}

build() {
	make
}

package() {
	make DESTDIR=$pkgdir install

	./zic -y ./yearistype -d $pkgdir/usr/share/zoneinfo ${timezones[@]}
	./zic -y ./yearistype -d $pkgdir/usr/share/zoneinfo/posix ${timezones[@]}
	./zic -y ./yearistype -d $pkgdir/usr/share/zoneinfo/right -L leapseconds ${timezones[@]}

	./zic -y ./yearistype -d $pkgdir/usr/share/zoneinfo -p America/New_York
	install -m444 -t $pkgdir/usr/share/zoneinfo iso3166.tab zone1970.tab zone.tab

	rm $pkgdir/etc/localtime
}
