# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=usb_modeswitch
pkgver=2.5.2
dataver=20170806
pkgrel=1
pkgdesc="Activating switchable USB devices on Linux"
arch=('x86_64' 'i586' 'aarch64')
url="http://www.draisberghof.de/usb_modeswitch/"
license=('GPL')
depends=('libusb')
backup=("etc/$pkgname.conf")
source=("http://www.draisberghof.de/usb_modeswitch/usb-modeswitch-${pkgver}.tar.bz2"
	"http://www.draisberghof.de/usb_modeswitch/usb-modeswitch-data-${dataver}.tar.bz2"
	"usb-modeswitch-${pkgver}-device_reference.txt::http://www.draisberghof.de/usb_modeswitch/device_reference.txt")
md5sums=('16b9a8efa1bf8fbd7d5612757eae4f26'
         'fb50d15b52e909d742dd16f0a9882316'
         '91f3b3638f013fd343bd98b251ce646f')

prepare() {
	cd $srcdir/usb-modeswitch-$pkgver
	sed -i 's|/usr/sbin/usb_mode|/usr/bin/usb_mode|g' usb_modeswitch.{conf,tcl,sh}
}

build() {
	cd usb-modeswitch-$pkgver
	make clean
	make SBINDIR=/usr/bin
}

package() {
	cd $srcdir/usb-modeswitch-$pkgver

	mkdir -p $pkgdir/usr/lib/udev
	mkdir -p $pkgdir/etc
	mkdir -p $pkgdir/usr/share/man/man1
	mkdir -p $pkgdir/usr/lib/systemd/system

	make \
		DESTDIR=$pkgdir \
		UDEVDIR=$pkgdir/usr/lib/udev \
		SBINDIR=$pkgdir/usr/bin \
		SYSDIR=$pkgdir/usr/lib/systemd/system \
		install

	cp $srcdir/usb-modeswitch-${pkgver}-device_reference.txt $pkgdir/etc/usb_modeswitch.setup

	cd $srcdir/usb-modeswitch-data-$dataver
	make DESTDIR=$pkgdir install RULESDIR=$pkgdir/usr/lib/udev/rules.d
}
