# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=('util-linux' 'util-linux-libs')
pkgbase=util-linux
_pkgmajor=2.32
pkgver=${_pkgmajor}.1
pkgrel=3
arch=('x86_64' 'i586' 'aarch64')
url="https://www.kernel.org/pub/linux/utils/util-linux/"
license=('GPL')
options=('strip' 'debug')
source=(https://www.kernel.org/pub/linux/utils/util-linux/v${_pkgmajor}/$pkgname-$pkgver.tar.xz)
md5sums=('9e5b1b8c1dc99455bdb6b462cf9436d9')

build() {
	if [ -f /usr/lib/libselinux.so ]; then
		export SELINUXOPTS="--with-selinux"
	else
		export SELINUXOPTS=''
	fi

	cd $pkgbase-$pkgver
	./configure \
		ADJTIME_PATH=/var/lib/hwclock/adjtime \
		--prefix=/usr \
		--libdir=/usr/lib \
		--bindir=/usr/bin \
		--localstatedir=/var \
		--enable-fs-paths-extra=/usr/bin \
		--with-btrfs \
		--without-python \
		--enable-kill \
		--enable-partx \
		--enable-raw \
		--enable-write \
		--disable-chfn-chsh \
		--disable-login \
		--disable-nologin \
		--disable-pylibmount \
		--disable-runuser \
		--disable-setpriv \
		--disable-su \
		--build=$CHOST $SELINUXOPTS
	make
}

package_util-linux() {
	pkgdesc="Miscellaneous system utilities for Linux"
	depends=('glibc' 'util-linux-libs' 'zlib' 'ncurses' 'systemd-libs')

	cd $pkgbase-$pkgver
	make DESTDIR=$pkgdir install

	cd $pkgdir
	mv {,usr/}sbin/* usr/bin
	rmdir sbin usr/sbin

	mkdir -p var/lib/hwclock

	rm -rf usr/lib/lib*.{a,so}*
}

package_util-linux-libs() {
	pkgdesc="Miscellaneous system libraries for Linux"
	depends=('glibc' 'libselinux')

	cd $pkgbase-$pkgver
	make DESTDIR=$pkgdir install-usrlib_execLTLIBRARIES
}
