# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgbase=util-linux
pkgname=('util-linux' 'util-linux-libs')
_pkgmajor=2.33
pkgver=${_pkgmajor}
pkgrel=6
arch=('x86_64' 'i586' 'aarch64' 'armv7h' 'armv6h' 'armv5tel' 'powerpc64le' 'powerpc64' 'powerpc' 'sparc64' 's390x' 'riscv64')
url="https://www.kernel.org/pub/linux/utils/util-linux/"
license=('GPL')
options=('strip' 'debug' 'staticlibs')
source=(https://www.kernel.org/pub/linux/utils/util-linux/v${_pkgmajor}/$pkgname-$pkgver.tar.xz)
md5sums=('3cdf1c652007610f92076ba4212381a9')

build() {
	cd $pkgbase-$pkgver
	./configure \
		ADJTIME_PATH=/var/lib/hwclock/adjtime \
		--prefix=/usr \
		--libdir=/usr/lib \
		--bindir=/usr/bin \
		--localstatedir=/var \
		--enable-fs-paths-extra=/usr/bin \
		--with-btrfs \
		--enable-kill \
		--enable-partx \
		--enable-raw \
		--enable-write \
		--disable-chfn-chsh \
		--disable-login \
		--disable-nologin \
		--disable-runuser \
		--disable-setpriv \
		--build=$CHOST
	make
}

package_util-linux() {
	pkgdesc="Miscellaneous system utilities for Linux"
	depends=('glibc' 'util-linux-libs' 'zlib' 'ncurses' 'systemd-libs')

	cd $pkgbase-$pkgver
	make DESTDIR=$pkgdir install

	cd $pkgdir
	mv {,usr/}sbin/* usr/bin
	rmdir sbin usr/sbin

	mkdir -p var/lib/hwclock

	rm -rf usr/lib/lib*.{a,so}*
}

package_util-linux-libs() {
	pkgdesc="Miscellaneous system libraries for Linux"
	depends=('glibc')

	cd $pkgbase-$pkgver
	make DESTDIR=$pkgdir install-usrlib_execLTLIBRARIES
}
