# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=vala
pkgver=0.42.2
bootver=0.40.8
pkgrel=1
pkgdesc="Compiler for the GObject type system"
arch=('x86_64' 'i586' 'aarch64' 'arm' 'mips64' 'mips' 'powerpc64le' 'powerpc64' 'powerpc' 'sparc64' 's390x' 'microblaze' 'riscv64' 'riscv32')
url="https://wiki.gnome.org/Projects/Vala"
license=('LGPL')
depends=('glib' 'pkgconf')
makedepends=('libxslt')
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"
	"http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${bootver%.*}/${pkgname}-${bootver}.tar.xz"
	'libvaladoc-Allow-disabling-the-graphviz-dependency.patch')
md5sums=('e28ad84b16351a3e41eacabdeadaaf8a'
         '3e3177692fb5d81a7b8aaa6b95a30bdd'
         'df766394c5421a833c25b765b334d362')

prepare() {
	cd $srcdir/$pkgname-$bootver
	sed -i '115d; 121,137d; 139,140d' configure.ac
	sed -i '/valadoc/d' Makefile.am
	ACLOCAL= autoreconf -fi

	cd $srcdir/$pkgname-$pkgver
	patch -p1 -i $srcdir/libvaladoc-Allow-disabling-the-graphviz-dependency.patch
	autoreconf -fi
}

build() {
	cd $srcdir/$pkgname-$bootver
	./configure \
		--prefix=$srcdir/vala-bootstrap \
		--build=$CHOST
	make
	make install

	export PATH="$srcdir/vala-bootstrap/bin:$PATH"

	cd $srcdir/$pkgname-$pkgver
	./configure \
		--prefix=/usr \
		--disable-graphviz \
		--build=$CHOST
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR=$pkgdir install
}
