# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=wpa_supplicant
pkgver=2.7
pkgrel=1
pkgdesc="A utility providing key negotiation for WPA wireless networks"
arch=('x86_64' 'i586' 'aarch64' 'armv7h' 'armv6h' 'armv5tel' 'powerpc64le' 'powerpc64' 'powerpc' 'sparc64' 's390x' 'riscv64')
url="http://hostap.epitest.fi/wpa_supplicant"
license=('GPL')
depends=('glibc' 'libressl' 'dbus' 'libnl')
source=("https://w1.fi/releases/${pkgname}-${pkgver}.tar.gz"
	'config')
md5sums=('a68538fb62766f40f890125026c42c10'
         '3124d9a6958b0c83c83bd03a72aee89b')

prepare() {
	cd $pkgname-$pkgver
	cp $srcdir/config $pkgname/.config
}

build() {
	cd $pkgname-$pkgver/$pkgname
	make LIBDIR=/usr/lib BINDIR=/usr/bin
}

package() {
	cd $pkgname-$pkgver/$pkgname
	make LIBDIR=/usr/lib BINDIR=/usr/bin DESTDIR=$pkgdir install

	install -d -m755 $pkgdir/etc/wpa_supplicant
	install -Dm644 wpa_supplicant.conf $pkgdir/usr/share/doc/wpa_supplicant/wpa_supplicant.conf

	install -d -m755 $pkgdir/usr/share/dbus-1/system-services
	install -m644 \
		dbus/fi.{epitest.hostap.WPASupplicant,w1.wpa_supplicant1}.service \
		$pkgdir/usr/share/dbus-1/system-services/

	install -Dm644 dbus/dbus-wpa_supplicant.conf $pkgdir/etc/dbus-1/system.d/wpa_supplicant.conf

	install -d -m755 $pkgdir/usr/lib/systemd/system
	install -m644 systemd/*.service $pkgdir/usr/lib/systemd/system/
}
