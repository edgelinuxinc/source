# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=zip
pkgver=3.0
apkgver=${pkgver/./}
pkgrel=1
pkgdesc="Compressor/archiver for creating and modifying zipfiles"
arch=('x86_64' 'aarch64')
url='http://www.info-zip.org/Zip.html'
license=('custom')
depends=('glibc' 'bzip2' 'bash')
source=("https://downloads.sourceforge.net/infozip/$pkgname$apkgver.tar.gz")
md5sums=('7b74551e63f8ee6aab6fbc86676c0d37')

prepare() {
	cd $pkgname$apkgver
	sed -e "/^CFLAGS_NOOPT =/s/\$/ $CFLAGS/" -i unix/Makefile
	sed -e "s/^LFLAGS1=''/LFLAGS1=$LDFLAGS/" -i unix/configure
}

build() {
	cd $pkgname$apkgver
	make -f unix/Makefile prefix=/usr generic_gcc
}

package() {
	cd $pkgname$apkgver
	make -f unix/Makefile \
		prefix=$pkgdir/usr \
		MANDIR=$pkgdir/usr/share/man/man1 \
		install
}
