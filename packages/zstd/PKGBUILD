# Maintainer: protonesso <nagakamira@gmail.com>
# Contributor: protonesso <nagakamira@gmail.com>

pkgname=zstd
pkgver=1.3.7
pkgrel=3
pkgdesc="Zstandard - Fast real-time compression algorithm"
arch=('x86_64' 'i586' 'aarch64' 'arm' 'riscv64' 'riscv32')
url="http://www.zstd.net/"
license=('GPL' 'BSD')
depends=('glibc' 'zlib' 'lz4' 'xz-utils')
source=("https://github.com/facebook/zstd/archive/v$pkgver.tar.gz"
	'threading.patch')
md5sums=('9b89923a360ac85a3b8076fdf495318d'
         'b7dc6c1f82ebca0cfa7ead9bfccfdecb')

prepare() {
	cd $pkgname-$pkgver
	patch -Np1 -i $srcdir/threading.patch
}

build() {
	cd $pkgname-$pkgver
	make
	make zstdmt
	make -C contrib/pzstd
}

package() {
	cd $pkgname-$pkgver
	make PREFIX=/usr DESTDIR=$pkgdir install

	install -Dm755 zstdmt $pkgdir/usr/bin/zstdmt
	install -Dm755 contrib/pzstd/pzstd $pkgdir/usr/bin/pzstd
}
